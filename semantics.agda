{-# OPTIONS --rewriting #-}

open import common
import accessibility
import syntactics
import reduction

module semantics
  (Level : Set)
  (_<_ : Level â†’ Level â†’ Set)
  (trans< : âˆ€ {i j k} â†’ i < j â†’ j < k â†’ i < k)
  (open accessibility Level _<_)
  (sup : âˆ€ i j â†’ âˆƒ[ k ] i < k Ã— j < k Ã— Acc k) where
open syntactics Level
open reduction Level

data U' k (U< : âˆ€ {j} â†’ j < k â†’ Term â†’ Set) : Term â†’ Set
el' : âˆ€ k (U< : âˆ€ {j} â†’ j < k â†’ Term â†’ Set) â†’ Term â†’ âˆ€ {T} â†’ U' k U< T â†’ Set

data U' k U< where
  UÌ‚ : âˆ€ j â†’ j < k â†’ U' k U< (ð’° j)
  âŠ¥Ì‚ : U' k U< mty
  Î Ì‚ : âˆ€ a â†’ (A : U' k U< a) â†’
      âˆ€ b â†’ (âˆ€ x â†’ el' k U< x A â†’ U' k U< (subst (x +: var) b)) â†’
      U' k U< (Î  a b)
  â‡’Ì‚  : âˆ€ a b â†’ a â‡’ b â†’ U' k U< b â†’ U' k U< a

el' k U< T (UÌ‚ j j<k) = U< j<k T
el' k U< _ âŠ¥Ì‚  = âŠ¥
el' k U< f (Î Ì‚ _ A _ B) = âˆ€ x â†’ (a : el' k U< x A) â†’ el' k U< ($áµˆ f x) (B x a)
el' k U< x (â‡’Ì‚  a b aâ‡’b A) = el' k U< x A

-- U' k and el' k are parametrized by U< j, where j < k;
-- we instantiate the parameters by induction on accessibility of levels

U<  : âˆ€ {k} â†’ Acc k â†’ âˆ€ {j} â†’ j < k â†’ Term â†’ Set
el< : âˆ€ {k} (p : Acc k) {j} (j<k : j < k) â†’ Term â†’ âˆ€ {T} â†’ U< p j<k T â†’ Set

U<  (acc< f) {j} j<k T = U'  j (U< (f j<k)) T
el< (acc< f) {j} j<k t = el' j (U< (f j<k)) t

-- We tie the knot by instantiating U< and el<

U : âˆ€ k (acc : Acc k) â†’ Term â†’ Set
U k acc T = U' k (U< acc) T

el : âˆ€ k (acc : Acc k) â†’ Term â†’ âˆ€ {T} â†’ U k acc T â†’ Set
el k acc t = el' k (U< acc) t

{----------------------------------------
  Irrelevance of accessibility across U
----------------------------------------}

accUâ‰¡ : âˆ€ {k T} (accâ‚ accâ‚‚ : Acc k) â†’ U k accâ‚ T â‰¡ U k accâ‚‚ T
accUâ‰¡ accâ‚ accâ‚‚ with refl â† (let open ext in accProp accâ‚ accâ‚‚) = refl

accU : âˆ€ {k T} (accâ‚ accâ‚‚ : Acc k) â†’ U k accâ‚ T â†’ U k accâ‚‚ T
accU accâ‚ accâ‚‚ = coe (accUâ‰¡ accâ‚ accâ‚‚)

accU< : âˆ€ {j k T} (accj : Acc j) (acck : Acc k) (j<k : j < k) â†’ U j accj T â†’ U< acck j<k T
accU< accj (acc< f) j<k = accU accj (f j<k)

{------------------------------------------
  Cumulativity:
  * Given j < k, U j can be lifted to U k
  * Given j < k and u : U j,
    the interpretation of u and
    the interpretation of the lifted u
    are equal
------------------------------------------}

cumU : âˆ€ {j k} (accj : Acc j) (acck : Acc k) â†’ j < k â†’ {T : Term} â†’
       U j accj T â†’ U k acck T

cumEl : âˆ€ {j k} (accj : Acc j) (acck : Acc k) (j<k : j < k) {t T : Term} (u : U j accj T) â†’
        el j accj t u â‰¡ el k acck t (cumU accj acck j<k u)

cumU (acc< f) (acc< g) j<k (UÌ‚ i i<j) = UÌ‚ i (trans< i<j j<k)
cumU _ _ _ âŠ¥Ì‚  = âŠ¥Ì‚
cumU accj acck j<k (Î Ì‚ a A b B) =
  Î Ì‚ a (cumU accj acck j<k A)
    b (Î» x a â†’
         let p = cumEl accj acck j<k A
         in cumU accj acck j<k (B x (coe (sym p) a)))
cumU accj acck j<k (â‡’Ì‚  a b aâ‡’b B) = â‡’Ì‚  a b aâ‡’b (cumU accj acck j<k B)

cumEl (acc< f) (acc< g) j<k (UÌ‚ i i<j) = accUâ‰¡ (f i<j) (g (trans< i<j j<k))
cumEl (acc< _) (acc< _) _ âŠ¥Ì‚  = refl
cumEl accj@(acc< _) acck@(acc< _) j<k {t = t} (Î Ì‚ a A b B) =
  let open ext in
  piext refl (Î» x â†’
    let p = cumEl accj acck j<k A in
    piext p (Î» a â†’
      let B' = Î» a â†’ cumU accj acck j<k (B x a)
          q = cumEl accj acck j<k (B x a)
      in trans q (cong (Î» a â†’ el' _ _ _ (B' a)) (sym (coe-Î² p a)))))
cumEl accj@(acc< _) acck@(acc< _) j<k (â‡’Ì‚  a b aâ‡’b B) = cumEl accj acck j<k B

{-------------------------------------------------------
  Propositional irrelevance of interpretations:
  two proofs of a âˆˆ ã€šAâŸ§â‚– are propositionally equal,
  even given different sets ã€šAâŸ§â‚– at different levels,
  for convertible A
-------------------------------------------------------}

-- First, irrelevance across U of the same level
elâ‰¡' : âˆ€ {k a Aâ‚ Aâ‚‚} (accâ‚ accâ‚‚ : Acc k)
       (uâ‚ : U k accâ‚ Aâ‚) (uâ‚‚ : U k accâ‚‚ Aâ‚‚) â†’
       Aâ‚ â‡” Aâ‚‚ â†’ el k accâ‚ a uâ‚ â‰¡ el k accâ‚‚ a uâ‚‚
elâ‰¡' (acc< f) (acc< g) (UÌ‚ _ j<kâ‚) (UÌ‚ _ j<kâ‚‚) ð’°â‚â‡”ð’°â‚‚
  with refl â† â‡”-ð’°-inv ð’°â‚â‡”ð’°â‚‚ = accUâ‰¡ (f j<kâ‚) (g j<kâ‚‚)
elâ‰¡' accâ‚ accâ‚‚ âŠ¥Ì‚ âŠ¥Ì‚ _ = refl
elâ‰¡' accâ‚ accâ‚‚ (Î Ì‚ aâ‚ Aâ‚ bâ‚ Bâ‚) (Î Ì‚ aâ‚‚ Aâ‚‚ bâ‚‚ Bâ‚‚) Î abâ‚â‡”Î abâ‚‚ =
  let aâ‚â‡”aâ‚‚ , bâ‚â‡”bâ‚‚ = â‡”-Î -inv Î abâ‚â‡”Î abâ‚‚
      open ext in
  piext refl (Î» x â†’
    let elaâ‰¡ = elâ‰¡' accâ‚ accâ‚‚ Aâ‚ Aâ‚‚ aâ‚â‡”aâ‚‚ in
    piext elaâ‰¡ (Î» a â†’
      elâ‰¡' accâ‚ accâ‚‚ (Bâ‚ x a) (Bâ‚‚ x (coe elaâ‰¡ a)) (â‡”-cong â‡”-refl bâ‚â‡”bâ‚‚)))
elâ‰¡' accâ‚ accâ‚‚ (â‡’Ì‚  aâ‚ aâ‚‚ aâ‚â‡’aâ‚‚ uâ‚) uâ‚‚ aâ‚â‡”aâ‚ƒ =
  elâ‰¡' accâ‚ accâ‚‚ uâ‚ uâ‚‚ (â‡”-trans (â‡”-sym (â‡’-â‡” aâ‚â‡’aâ‚‚)) aâ‚â‡”aâ‚ƒ)
elâ‰¡' accâ‚ accâ‚‚ uâ‚ (â‡’Ì‚  aâ‚‚ aâ‚ƒ aâ‚‚â‡’aâ‚ƒ uâ‚‚) aâ‚â‡”aâ‚‚ =
  elâ‰¡' accâ‚ accâ‚‚ uâ‚ uâ‚‚ (â‡”-trans aâ‚â‡”aâ‚‚ (â‡’-â‡” aâ‚‚â‡’aâ‚ƒ))
elâ‰¡' _ _ (UÌ‚ _ _) âŠ¥Ì‚ ð’°â‡”mty with () â† â‡”Ì¸â‹†-ð’°mty ð’°â‡”mty
elâ‰¡' _ _ (UÌ‚ _ _) (Î Ì‚ _ _ _ _) ð’°â‡”Î  with () â† â‡”Ì¸â‹†-ð’°Î  ð’°â‡”Î 
elâ‰¡' _ _ âŠ¥Ì‚ (Î Ì‚ _ _ _ _) mtyâ‡”Î  with () â† â‡”Ì¸â‹†-mtyÎ  mtyâ‡”Î 
elâ‰¡' _ _ âŠ¥Ì‚ (UÌ‚ _ _) mtyâ‡”ð’° with () â† â‡”Ì¸â‹†-ð’°mty (â‡”-sym mtyâ‡”ð’°)
elâ‰¡' _ _ (Î Ì‚ _ _ _ _) (UÌ‚ _ _) Î â‡”ð’° with () â† â‡”Ì¸â‹†-ð’°Î  (â‡”-sym Î â‡”ð’°)
elâ‰¡' _ _ (Î Ì‚ _ _ _ _) âŠ¥Ì‚ Î â‡”mty with () â† â‡”Ì¸â‹†-mtyÎ  (â‡”-sym Î â‡”mty)

-- Cumulativity and the existence of suprema
-- gives us irrelevance across different levels
elâ‰¡ : âˆ€ {kâ‚ kâ‚‚} (acckâ‚ : Acc kâ‚) (acckâ‚‚ : Acc kâ‚‚) {t Tâ‚ Tâ‚‚ : Term}
         (uâ‚ : U kâ‚ acckâ‚ Tâ‚) (uâ‚‚ : U kâ‚‚ acckâ‚‚ Tâ‚‚) â†’
         Tâ‚ â‡” Tâ‚‚ â†’ el kâ‚ acckâ‚ t uâ‚ â‰¡ el kâ‚‚ acckâ‚‚ t uâ‚‚
elâ‰¡ {kâ‚} {kâ‚‚} acckâ‚ acckâ‚‚ uâ‚ uâ‚‚ Tâ‚â‡”Tâ‚‚ =
  let â„“ , kâ‚<â„“ , kâ‚‚<â„“ , accâ„“ = sup kâ‚ kâ‚‚
  in begin
    el kâ‚ acckâ‚ _ uâ‚                        â‰¡âŸ¨ cumEl acckâ‚ accâ„“ kâ‚<â„“ uâ‚ âŸ©
    el â„“  accâ„“  _ (cumU acckâ‚ accâ„“ kâ‚<â„“ uâ‚) â‰¡âŸ¨ elâ‰¡' accâ„“ accâ„“
                                                      (cumU acckâ‚ accâ„“ kâ‚<â„“ uâ‚)
                                                      (cumU acckâ‚‚ accâ„“ kâ‚‚<â„“ uâ‚‚) Tâ‚â‡”Tâ‚‚ âŸ©
    el â„“  accâ„“  _ (cumU acckâ‚‚ accâ„“ kâ‚‚<â„“ uâ‚‚) â‰¡âŸ¨ sym (cumEl acckâ‚‚ accâ„“ kâ‚‚<â„“ uâ‚‚) âŸ©
    el kâ‚‚ acckâ‚‚ _ uâ‚‚ âˆŽ

-- elâ‰¡ specialized to identical syntactic types
elâ†’ : âˆ€ {kâ‚ kâ‚‚} (acckâ‚ : Acc kâ‚) (acckâ‚‚ : Acc kâ‚‚) {t T : Term}
      (uâ‚ : U kâ‚ acckâ‚ T) (uâ‚‚ : U kâ‚‚ acckâ‚‚ T) â†’
      el kâ‚ acckâ‚ t uâ‚ â†’ el kâ‚‚ acckâ‚‚ t uâ‚‚
elâ†’ acckâ‚ acckâ‚‚ uâ‚ uâ‚‚ = coe (elâ‰¡ acckâ‚ acckâ‚‚ uâ‚ uâ‚‚ â‡”-refl)

-- elâ‰¡ specialized to identical proofs of accessibility
â‡”-el : âˆ€ {k a A B} (acc : Acc k)
       (uA : U k acc A) (uB : U k acc B) (Aâ‡”B : A â‡” B) â†’
       el k acc a uA â†’ el k acc a uB
â‡”-el {k} acc uA uB Aâ‡”B = coe (elâ‰¡ acc acc uA uB Aâ‡”B)

-- Could use â‡”-el since A â‰¡ B â†’ A â‡” B by â‡”-refl, but that's a little silly
â‰¡-el : âˆ€ {k t A A'} acc (u : U k acc A) (p : A â‰¡ A') â†’
       el k acc t u â†’ el k acc t (transp (U k acc) p u)
â‰¡-el acc u refl elA = elA

{-------------------
  Inversion lemmas
--------------------}

-- Universes are Ã  la Russell
el-U : âˆ€ {j k A} (acc : Acc k) (u : U k acc (ð’° j)) â†’ el k acc A u â†’ Î£[ acc' âˆˆ Acc j ] j < k Ã— U j acc' A
el-U (acc< f) (UÌ‚ j j<k) elU = f j<k , j<k , elU
el-U acc (â‡’Ì‚  (ð’° j) (ð’° j) (â‡’-ð’° j) u) elU = el-U acc u elU

-- Nothing lives in the empty type
empty : âˆ€ {k t} acc (u : U k acc mty) â†’ el k acc t u â†’ âŠ¥
empty acc âŠ¥Ì‚  ()
empty acc (â‡’Ì‚  mty mty â‡’â‹†-mty u) = empty acc u

-- Inversion on semantic function type
invÎ -U : âˆ€ {a b k} (acc : Acc k) â†’ U k acc (Î  a b) â†’
         Î£[ A âˆˆ U k acc a ] âˆ€ x â†’ el k acc x A â†’ U k acc (subst (x +: var) b)
invÎ -U acc (Î Ì‚ a A b B) = A , B
invÎ -U acc (â‡’Ì‚  (Î  a b) (Î  a' b') (â‡’-Î  aâ‡’a' bâ‡’b') u) =
  let A' , B' = invÎ -U acc u
  in â‡’Ì‚  a a' aâ‡’a' A' , Î» x elA â†’ â‡’Ì‚  _ _ (â‡’-cong (â‡’-refl x) bâ‡’b') (B' x elA)

-- Inversion on semantic functions
invÎ -el : âˆ€ {a b k} (acc : Acc k) (u : U k acc (Î  a b)) f â†’ el k acc f u â†’
          let A , B = invÎ -U acc u in
          âˆ€ x â†’ (a : el k acc x A) â†’ el k acc ($áµˆ f x) (B x a)
invÎ -el acc (Î Ì‚ a A b B) f elB x elA = elB x elA
invÎ -el acc (â‡’Ì‚  (Î  a b) (Î  a' b') (â‡’-Î  aâ‡’a' bâ‡’b') u) = invÎ -el acc u

{----------------------------------------------------
  Backward preservation of U, el with respect to â‡’â‹†
----------------------------------------------------}

â‡’â‹†-U : âˆ€ {k} (acc : Acc k) {a b} â†’ a â‡’â‹† b â†’ U k acc b â†’ U k acc a
â‡’â‹†-U _ (â‡’â‹†-refl a) u = u
â‡’â‹†-U acc (â‡’â‹†-trans aâ‡’b bâ‡’â‹†c) u = â‡’Ì‚ _ _ aâ‡’b (â‡’â‹†-U acc bâ‡’â‹†c u)

â‡’-el : âˆ€ {k} (acc : Acc k) {a b A} (u : U k acc A) â†’ a â‡’ b â†’ el k acc b u â†’ el k acc a u
â‡’-el (acc< f) (UÌ‚ j j<k) aâ‡’b = â‡’â‹†-U (f j<k) (â‡’-â‡’â‹† aâ‡’b)
â‡’-el acc (Î Ì‚ _ A _ B) aâ‡’b elB x elA = â‡’-el acc (B x elA) (â‡’-$áµˆ aâ‡’b (â‡’-refl x)) (elB x elA)
â‡’-el acc (â‡’Ì‚  A B Aâ‡’B u) aâ‡’b = â‡’-el acc u aâ‡’b

â‡’â‹†-el : âˆ€ {k} (acc : Acc k) {a b A} (u : U k acc A) â†’ a â‡’â‹† b â†’ el k acc b u â†’ el k acc a u
â‡’â‹†-el acc u (â‡’â‹†-refl a) elU = elU
â‡’â‹†-el acc u (â‡’â‹†-trans aâ‡’b bâ‡’â‹†c) elU = â‡’-el acc u aâ‡’b (â‡’â‹†-el acc u bâ‡’â‹†c elU)

{---------------------------------
  Subject reduction of U:
  if a â‡’â‹† b and U k a then U k b
---------------------------------}

SRU  : âˆ€ {k} (acc : Acc k) {a b} â†’ a â‡’ b â†’ U k acc a â†’ U k acc b
SRU acc (â‡’-ð’° _) (UÌ‚ _ j<k) = UÌ‚ _ j<k
SRU acc â‡’-mty âŠ¥Ì‚ = âŠ¥Ì‚
SRU acc (â‡’-Î  {a' = a'} {b' = b'} aâ‡’a' bâ‡’b') (Î Ì‚ a A b B) =
  Î Ì‚ a' (SRU acc aâ‡’a' A)
    b' (Î» x elA â†’ SRU acc (â‡’-cong (â‡’-refl x) bâ‡’b')
          (B x (â‡”-el acc (SRU acc aâ‡’a' A) A (â‡”-sym (â‡’-â‡” aâ‡’a')) elA)))
SRU acc {b = b} aâ‡’b (â‡’Ì‚  a c aâ‡’c C) =
  let d , bâ‡’d , câ‡’d = diamond aâ‡’b aâ‡’c
  in â‡’Ì‚  b d bâ‡’d (SRU acc câ‡’d C)

SRUâ‹† : âˆ€ {k a b} acc â†’ a â‡’â‹† b â†’ U k acc a â†’ U k acc b
SRUâ‹† acc (â‡’â‹†-refl a) u = SRU acc (â‡’-refl a) u
SRUâ‹† acc (â‡’â‹†-trans aâ‡’b bâ‡’â‹†c) u = SRUâ‹† acc bâ‡’â‹†c (SRU acc aâ‡’b u)

â‡”-U : âˆ€ {k a b} acc â†’ a â‡” b â†’ U k acc a â†’ U k acc b
â‡”-U acc (_ , aâ‡’â‹†c , bâ‡’â‹†c) u = â‡’â‹†-U acc bâ‡’â‹†c (SRUâ‹† acc aâ‡’â‹†c u)

{-----------------------------------------
  Semantic well-formedness:
    Ïƒ âˆˆ âŸ¦Î“âŸ§ = x â¦‚ A # k âˆˆ Î“ â†’ Ïƒ x âˆˆ âŸ¦AâŸ§â‚–
-----------------------------------------}

data V : Ctxt â†’ Set
em : âˆ€ (Ïƒ : Nat â†’ Term) {Î“} â†’ V Î“ â†’ Set

data V where
  âˆ™Ì‚  : V âˆ™
  âˆ·Ì‚  : âˆ€ {Î“ A} (v : V Î“) â†’ (âˆ€ Ïƒ â†’ em Ïƒ v â†’ âˆƒ[ k ] Î£[ acc âˆˆ Acc k ] U k acc (subst Ïƒ A)) â†’ V (Î“ âˆ· A)

em Ïƒ âˆ™Ì‚  = âŠ¤
em Ïƒ (âˆ·Ì‚  v w) =
  Î£[ emV âˆˆ em (Ïƒ âˆ˜ suc) v ]
    let k , acc , u = w (Ïƒ âˆ˜ suc) emV
    in el k acc (Ïƒ 0) u