embed
  {{ tex-preamble
    \newcommand{\gap}{\:}
    \newcommand{\kw}[1]{ \mathsf{#1} }
  }}

metavar x, y, z, f, g ::=
  {{ com term variables }}
  {{ lex alphanum }}

metavar i, j ::=
  {{ com level variables }}
  {{ lex numeral }}

grammar

a, b, c, A, B, C, k, l {{ tex \ell }} :: 'tm_' ::= {{ com terms }}
  | x :: :: Var
    {{ com variable }}
  | ! x :: :: Const
    {{ com constant }}
    {{ tex \kw{[[x]]} }}
  | λ x : A . b :: :: Abs
    (+ bind x in b +)
    {{ com function }}
    {{ tex \lambda [[x]] \mathbin{:} [[A]] \mathpunct{.} [[b]] }}
  | λ x . b :: S :: UAbs
    {{ com unannotated functions }}
    {{ tex \lambda [[x]] \mathpunct{.} [[b]] }}
  | Π x : A . B :: :: Pi
    (+ bind x in B +)
    {{ com function type }}
    {{ tex \Pi [[x]] \mathbin{:} [[A]] \mathpunct{.} [[B]] }}
  | A → B :: S :: Fun
    {{ com nondependent function type }}
    {{ tex [[A]] \to [[B]] }}
  | b a :: :: App
    {{ com function application }}
    {{ tex [[b]] \gap [[a]] }}
  | ⊥ :: :: Mty
    {{ com empty type }}
    {{ tex \bot }}
  | abs b :: S :: Exf
    {{ com ex falso quodlibet }}
    {{ tex \kw{absurd} \gap [[b]] }}
  | abs' A b :: :: Exf'
    {{ com explicitly annotated ex falso }}
    {{ tex \kw{absurd}_{[[A]]} \gap [[b]] }}
  | a = b :: :: Eq
    {{ com equality type }}
    {{ tex [[a]] = [[b]] }}
  | Type k :: :: Type
    {{ com type universe }}
    {{ tex \kw{U} \gap [[k]] }}
  | U k :: :: Univ
    {{ com concrete type universe }}
    {{ tex \kw{U}_{[[k]]} }}
  | Level< l :: :: Lvl
    {{ com level type }}
    {{ tex \kw{Level}\texttt{<} \gap [[l]] }}
  | i :: :: Lof
    {{ com concrete level }}
    {{ tex [[i]] }}
  | o :: :: Omega
    {{ com omega level }}
    {{ tex \omega }}
  | ↑ k :: :: Succ
    {{ com level successor }}
    {{ tex \mathop{\uparrow} [[k]] }}
  | k ++ :: S :: PlusOne
    {{ com level meta successor }}
    {{ tex [[k]] + 1 }}
  | k ⊔ l :: :: Sup
    {{ com level maximum }}
    {{ tex [[k]] \sqcup [[l]] }}
  | ∀ x . A :: S :: PPi
    {{ com level polymorphism }}
    {{ tex \forall [[x]] \mathpunct{.} [[A]] }}
  | ∀ x < k . A :: S :: BPPi
    {{ com bounded level polymorphism }}
    {{ tex \forall [[x]] < [[k]] \mathpunct{.} [[A]] }}
  | Λ x . a :: S :: PAbs
    {{ com level abstraction }}
    {{ tex \Lambda [[x]] \mathpunct{.} [[a]] }}
  | a [ k ] :: S :: PApp
    {{ com level application }}
    {{ tex [[a]] \gap [ [[k]] ]}}
  | wf c :: :: Wf
    {{ com well-founded level induction }}
    {{ tex \kw{wf} \gap [[c]] }}
  | b { x ↦ a } :: S :: Subst
    {{ com single substitution }}
    {{ tex [[b]][ [[x]] \mapsto [[a]] ]}}
  | b { s } :: S :: Substs
    {{ com simultaneous substitution }}
    {{ tex [[b]][ [[s]] ]}}
  | _ :: S :: Hole
    {{ com hole }}
    {{ tex \relax }}
  | ( a ) :: S :: Parens
    {{ com parentheses }}
    {{ tex ([[a]]) }}
  | (# a #) :: S :: SParens
    {{ com invisible parentheses }}
    {{ tex [[a]] }}
  | a ^ T :: M :: Taka
    {{ com Takahashi's complete development }}
    {{ tex [[a]]^{ \mathsf{T} } }}

s {{ tex \sigma }} :: 'subst_' ::= {{ com substitutions }}
  | • :: :: Nil
    {{ com identity substitution }}
    {{ tex \cdot }}
  | s , x ↦ a :: :: Cons
    {{ com substitution extension }}
    {{ tex [[s]], [[x]] \mapsto [[a]] }}
v :: 'val_' ::= {{ com values }}
  | λ x : A . b :: :: Abs
    (+ bind x in b +)
  | Π x : A . B :: :: Pi
    (+ bind x in b +)
  | ⊥ :: :: Mty
  | Type k :: :: Univ
  | Level< l :: :: Lvl
  | i :: :: Lof

G {{ tex \Gamma }}, D {{ tex \Delta }} :: 'ctxt_' ::= {{ com contexts }}
  | • :: :: Nil
    {{ com empty context }}
    {{ tex \cdot }}
  | x : A :: S :: Singleton
    {{ com singleton context }}
    {{ tex [[x]] \mathbin{:} [[A]] }}
  | G , x : A :: :: Cons
    {{ com context extension }}
    {{ tex [[G]], [[x]] \mathbin{:} [[A]] }}
  | G , D :: M :: Append
    {{ com context append }}
    {{ tex [[G]], [[D]] }}

P, Q :: 'set_' ::= {{ com sets }}
  | ∅ :: :: Empty
    {{ tex \varnothing }}
  | { x | formula } :: :: Builder
    (+ bind x in formula +)
    {{ tex \lbrace [[x]] \mid [[formula]] \rbrace }}
  | { i , x | formula } :: :: IBuilder
    (+ bind i union x in formula +)
    {{ tex \lbrace [[x]] \mid [[formula]] \rbrace_{[[i]]} }}
  | I i :: :: Idx
    {{ tex [[I]] \gap [[i]] }}

R :: 'rel_' ::= {{ com relation }}

formula :: 'formula_' ::=
  | judgement :: :: judgement
  | formula1 ∧ formula2 :: :: and
    {{ tex [[formula1]] \wedge [[formula2]] }}
  | formula1 ∨ formula2 :: :: or
    {{ tex [[formula1]] \vee [[formula2]] }}
  | formula1 → formula2 :: :: implies
    {{ tex [[formula1]] \to [[formula2]] }}
  | ∀ x . formula :: :: forall
    {{ tex \forall [[x]] \mathpunct{.} [[formula]] }}
  | ∀ P . formula :: :: setforall
    {{ tex \forall [[P]] \mathpunct{.} [[formula]] }}
  | ∃ i . formula :: :: exists
    {{ tex \exists [[i]] \mathpunct{.} [[formula]] }}
  | ∃ P . formula :: :: setexists
    {{ tex \exists [[P]] \mathpunct{.} [[formula]] }}
  | i < j :: S :: lt
    {{ tex [[i]] < [[j]] }}
  | i ≤ j :: S :: le
    {{ tex [[i]] \leq [[j]] }}
  | x : A ∈ G :: S :: mem
    {{ tex [[x]] \mathrel{:} [[A]] \in [[G]] }}
  | ⊢ G :: S :: ctxt
    {{ tex \mathop{\vdash} [[G]] }}
  | G ⊢ a : A :: S :: typing
    {{ tex [[G]] \vdash [[a]] \mathrel{:} [[A]] }}
  | s ⊧ G :: S :: wsubst
    {{ tex [[s]] \vDash [[G]] }}
  | G ⊧ a : A :: S :: wtyping
    {{ tex [[G]] \vDash [[a]] : [[A]] }}
  | A = B :: S :: defeq
    {{ tex [[A]] \equiv [[B]] }}
  | a ⇒ b :: S :: par
    {{ tex [[a]] \Rightarrow [[b]] }}
  | a ⇒* b :: S :: pars
    {{ tex [[a]] \Rightarrow^\ast [[b]] }}
  | a ⇔ b :: S :: conv
    {{ tex [[a]] \Leftrightarrow [[b]] }}
  | a ⇝ b :: S :: cbn
    {{ tex [[a]] \rightsquigarrow [[b]] }}
  | a ⇝* b :: S :: cbns
    {{ tex [[a]] \rightsquigarrow^\ast [[b]] }}
  | ⟦ A ⟧ i , I ↘ P :: S :: interp
    {{ tex \mathopen{\llbracket} [[A]] \mathclose{\rrbracket}_{[[i]]} \gap [[I]] \searrow [[P]] }}
  | ⟦ A ⟧ i ↘ P :: S :: interps
    {{ tex \mathopen{\llbracket} [[A]] \mathclose{\rrbracket}_{[[i]]} \searrow [[P]] }}
  | a ∈ P :: S :: in
    {{ tex [[a]] \in [[P]] }}
  | R ( x , P ) :: S :: rel
    {{ tex [[R]]([[x]], [[P]]) }}
  | G ⊢ A ≤ B :: S :: subtyping
    {{ tex [[G]] \vdash [[A]] \preccurlyeq [[B]] }}
  | ( formula ) :: S :: parentheses
    {{ tex ([[formula]]) }}

subrules
  v <:: a