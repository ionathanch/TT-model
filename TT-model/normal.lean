import Â«TT-modelÂ».tactics
import Â«TT-modelÂ».syntactics
import Â«TT-modelÂ».reduction

open Term

set_option autoImplicit false
set_option pp.fieldNotation false

variable [LevelClass]

/-*-------------------------------------------------------
  Normal and neutral forms:
  * Normal forms are constructors with normal components
    and all neutral forms
  * Neutral forms are destructors with neutral heads
    and all variables
-------------------------------------------------------*-/

mutual
@[simp]
def nf : Term â†’ Prop
  | ð’° a => nf a
  | pi a b => nf a âˆ§ nf b
  | abs b => nf b
  | app b a => ne b âˆ§ nf a
  | exf b => ne b
  | lvl a => nf a
  | _ => True

@[simp]
def ne : Term â†’ Prop
  | var _ => True
  | app b a => ne b âˆ§ nf a
  | exf b => ne b
  | _ => False
end

theorem neNf {a} : ne a â†’ nf a := by
  induction a <;> unfold ne nf <;> simp
  case app =>
    intro neb nfa
    unfold nf
    exact âŸ¨neb, nfaâŸ©

-- Normal and neutral forms are stable under renaming
-- (other direction also holds but is unneeded)
mutual
theorem nfRename {Î¾ a} (nfa : nf (rename Î¾ a)) : nf a := by
  cases a <;> simp at *
  case ð’° => exact nfRename nfa
  case pi => let âŸ¨nfa, nfbâŸ© := nfa; exact âŸ¨nfRename nfa, nfRename nfbâŸ©
  case abs => exact nfRename nfa
  case app => let âŸ¨nfb, nfaâŸ© := nfa; exact âŸ¨neRename nfb, nfRename nfaâŸ©
  case exf => exact neRename nfa
  case lvl => exact nfRename nfa

theorem neRename {Î¾ a} (nfa : ne (rename Î¾ a)) : ne a := by
  cases a <;> simp at *
  case app => let âŸ¨neb, nfaâŸ© := nfa; exact âŸ¨neRename neb, nfRename nfaâŸ©
  case exf => exact neRename nfa
end

/-*------------------------------------------
  Weakly normal and neutral forms,
  i.e. existence of some parallel reduction
       to a normal or neutral form.
------------------------------------------*-/

@[simp] def wnf (a : Term) : Prop := âˆƒ b, nf b âˆ§ a â‡’â‹† b
@[simp] def wne (a : Term) : Prop := âˆƒ b, ne b âˆ§ a â‡’â‹† b

-- Weak normal forms are backwards closed by reduction
theorem wnfPar {a b} (r : a â‡’ b) : wnf b â†’ wnf a
  | âŸ¨c, nfc, rcâŸ© => âŸ¨c, nfc, Pars.trans r rcâŸ©

-- Constructor for weak normal forms
-- (other constructors are unneeded)
theorem wnfAbs {b} : wnf b â†’ wnf (abs b)
  | âŸ¨c, nfc, rcâŸ© => by
    exact âŸ¨abs c, nfc, parsAbs rcâŸ©

-- Inversion principle for weak normal forms
-- (other constructors are uneeded)
theorem wnfAppInv {b s} : wnf (app b (var s)) â†’ wnf b
  | âŸ¨c, nfc, râŸ© => by
    generalize e : app b (var s) = b' at r
    induction r generalizing b; subst e
    case refl b =>
      let âŸ¨neb, _âŸ© := nfc
      exact âŸ¨b, neNf neb, Pars.refl _âŸ©
    case trans b' c ra rb' _ =>
      subst e; cases ra
      case Î² rb ra _ =>
        cases ra
        rw [substToRename] at rb'
        have rb' := Pars.trans (parRename (s +: id) rb) rb'
        let âŸ¨c, e, rbâŸ© := parsAntirenaming rb'; subst e
        exact wnfAbs âŸ¨c, nfRename nfc, rbâŸ©
      case app rb ra ih =>
        cases ra
        apply wnfPar rb (ih nfc rfl)
