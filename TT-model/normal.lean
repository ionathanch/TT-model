import Â«TT-modelÂ».tactics
import Â«TT-modelÂ».syntactics
import Â«TT-modelÂ».reduction

open Term

set_option autoImplicit false
set_option pp.fieldNotation false

variable [LevelClass]

/-*-------------------------------------------------------
  Normal and neutral forms:
  * Normal forms are constructors with normal components
    and all neutral forms
  * Neutral forms are destructors with neutral heads
    and all variables
-------------------------------------------------------*-/

mutual
@[simp]
def nf : Term â†’ Prop
  | ð’° a => nf a
  | pi a b => nf a âˆ§ nf b
  | abs b => nf b
  | app b a => ne b âˆ§ nf a
  | exf b => ne b
  | lvl a => nf a
  | _ => True

@[simp]
def ne : Term â†’ Prop
  | var _ => True
  | app b a => ne b âˆ§ nf a
  | exf b => ne b
  | _ => False
end

theorem neNf {a} : ne a â†’ nf a := by
  induction a <;> unfold ne nf <;> simp
  case app =>
    intro neb nfa
    unfold nf
    exact âŸ¨neb, nfaâŸ©

/-*---------------------------------------------------
  Normal and neutral forms are stable under renaming
  (both directions hold but we only need one)
---------------------------------------------------*-/

mutual
theorem nfRename {Î¾ a} (nfa : nf (rename Î¾ a)) : nf a := by
  cases a <;> simp at *
  case ð’° | abs | lvl => exact nfRename nfa
  case pi => let âŸ¨nfa, nfbâŸ© := nfa; exact âŸ¨nfRename nfa, nfRename nfbâŸ©
  case app => let âŸ¨nfb, nfaâŸ© := nfa; exact âŸ¨neRename nfb, nfRename nfaâŸ©
  case exf => exact neRename nfa

theorem neRename {Î¾ a} (nfa : ne (rename Î¾ a)) : ne a := by
  cases a <;> simp at *
  case app => let âŸ¨neb, nfaâŸ© := nfa; exact âŸ¨neRename neb, nfRename nfaâŸ©
  case exf => exact neRename nfa
end

/-*-------------------------------------------------
  Forward preservation of normal and neutral forms
-------------------------------------------------*-/

mutual
theorem nfPar {a b} (r : a â‡’ b) : nf a â†’ nf b := by
  cases r <;> simp <;> intros
  case ð’° ra nfa | abs ra nfa | lvl ra nfa => exact nfPar ra nfa
  case pi ra rb nfa nfb => exact âŸ¨nfPar ra nfa, nfPar rb nfbâŸ©
  case app rb ra neb nfa => exact âŸ¨nePar rb neb, nfPar ra nfaâŸ©
  case exf rb neb => exact nePar rb neb

theorem nePar {a b} (r : a â‡’ b) : ne a â†’ ne b := by
  cases r <;> simp <;> intros
  case app rb ra neb nfa => exact âŸ¨nePar rb neb, nfPar ra nfaâŸ©
  case exf rb neb => exact nePar rb neb
end

theorem nfPars {a b} (r : a â‡’â‹† b) : nf a â†’ nf b := by
  induction r
  case refl => simp
  case trans ra _ ih => intro nfa; exact ih (nfPar ra nfa)

theorem nePars {a b} (r : a â‡’â‹† b) : ne a â†’ ne b := by
  induction r
  case refl => simp
  case trans ra _ ih => intro nfa; exact ih (nePar ra nfa)

/-*------------------------------------------
  Weakly normal and neutral forms,
  i.e. existence of some parallel reduction
       to a normal or neutral form.
------------------------------------------*-/

@[simp] def wnf (a : Term) : Prop := âˆƒ b, nf b âˆ§ a â‡’â‹† b
@[simp] def wne (a : Term) : Prop := âˆƒ b, ne b âˆ§ a â‡’â‹† b

theorem wneWnf {a} : wne a â†’ wnf a
  | âŸ¨b, neb, rbâŸ© => âŸ¨b, neNf neb, rbâŸ©

theorem nfWnf {a} (nfa : nf a) : wnf a := âŸ¨a, nfa, Pars.refl aâŸ©
theorem neWne {a} (nea : ne a) : wne a := âŸ¨a, nea, Pars.refl aâŸ©

/-*----------------------------------------------------
  Weak normal/neutral forms are stable under renaming
----------------------------------------------------*-/

theorem wnfRename {Î¾ a} : wnf (rename Î¾ a) â†’ wnf a
  | âŸ¨b, nfb, rbâŸ© => by
    let âŸ¨c, e, râŸ© := parsAntirenaming rb; subst e
    exact âŸ¨c, nfRename nfb, râŸ©

theorem wneRename {Î¾ a} : wne (rename Î¾ a) â†’ wne a
  | âŸ¨b, neb, rbâŸ© => by
    let âŸ¨c, e, râŸ© := parsAntirenaming rb; subst e
    exact âŸ¨c, neRename neb, râŸ©

/-*-----------------------------------------------------------
  Forward/backward preservation of weak normal/neutral forms
-----------------------------------------------------------*-/

theorem wnfBwd {a b} (r : a â‡’ b) : wnf b â†’ wnf a
  | âŸ¨c, nfc, rcâŸ© => âŸ¨c, nfc, Pars.trans r rcâŸ©

theorem wneBwds {a b} (r : a â‡’â‹† b) : wne b â†’ wne a
  | âŸ¨c, nec, rcâŸ© => âŸ¨c, nec, parsTrans r rcâŸ©

theorem wnfFwds {a b} (r : a â‡’â‹† b) : wnf a â†’ wnf b
  | âŸ¨c, nfc, rcâŸ© => by
    let âŸ¨d, rbd, rcdâŸ© := confluence r rc
    refine âŸ¨d, nfPars rcd nfc, rbdâŸ©

theorem wneFwds {a b} (r : a â‡’â‹† b) : wne a â†’ wne b
  | âŸ¨c, nec, rcâŸ© => by
    let âŸ¨d, rbd, rcdâŸ© := confluence r rc
    refine âŸ¨d, nePars rcd nec, rbdâŸ©

/-*-------------------------------------------
  Constructors for weak normal/neutral forms
-------------------------------------------*-/

theorem wnfPi {a b} (wnfa : wnf a) (wnfb : wnf b) : wnf (pi a b) :=
  let âŸ¨a', nfa, raâŸ© := wnfa
  let âŸ¨b', nfb, rbâŸ© := wnfb
  âŸ¨pi a' b', âŸ¨nfa, nfbâŸ©, parsPi ra rbâŸ©

theorem wnfAbs {b} (wnfb : wnf b) : wnf (abs b) :=
  let âŸ¨c, nfc, rcâŸ© := wnfb
  âŸ¨abs c, nfc, parsAbs rcâŸ©

theorem wneApp {b a} (wneb : wne b) (wnfa : wnf a) : wne (app b a) :=
  let âŸ¨b', neb, rbâŸ© := wneb
  let âŸ¨a', nfa, raâŸ© := wnfa
  âŸ¨app b' a', âŸ¨neb, nfaâŸ©, parsApp rb raâŸ©

theorem wneVar {s} : wne (var s) :=
  âŸ¨var s, âŸ¨âŸ©, Pars.refl _âŸ©

theorem wneExf {b} (wneb : wne b) : wne (exf b) :=
  let âŸ¨c, nfc, rcâŸ© := wneb
  âŸ¨exf c, nfc, parsExf rcâŸ©

theorem wneLof {a j} (r : a â‡’â‹† lof j) (wnea : wne a) : False :=
  match wneFwds r wnea with
  | âŸ¨b, neb, rbâŸ© => by rw [parsLofInv rb] at neb; simp at neb

-- Inversion principle for weak normal applications to variables
theorem wnfAppInv {b s} : wnf (app b (var s)) â†’ wnf b
  | âŸ¨c, nfc, râŸ© => by
    generalize e : app b (var s) = b' at r
    induction r generalizing b; subst e
    case refl b =>
      let âŸ¨neb, _âŸ© := nfc
      exact âŸ¨b, neNf neb, Pars.refl _âŸ©
    case trans b' c ra rb' _ =>
      subst e; cases ra
      case Î² rb ra _ =>
        cases ra
        rw [substToRename] at rb'
        have rb' := Pars.trans (parRename (s +: id) rb) rb'
        let âŸ¨c, e, rbâŸ© := parsAntirenaming rb'; subst e
        exact wnfAbs âŸ¨c, nfRename nfc, rbâŸ©
      case app rb ra ih =>
        cases ra
        apply wnfBwd rb (ih nfc rfl)
