import Â«TT-modelÂ».syntactics
import Â«TT-modelÂ».reduction

open Term

set_option autoImplicit false
set_option pp.fieldNotation false

variable [lc : LevelClass]

-- Interpretation interface
@[simp] def IInterp (a : Term) (P : Term â†’ Prop) : Prop :=
  âˆƒ (I : Term â†’ (Term â†’ Prop) â†’ Prop),
    I a P âˆ§ (âˆ€ {a b}, a â‡’â‹† b â†’ I b P â†’ I a P)

-- Interpretations of Type types
inductive Interp (i : lc.L) (I : âˆ€ j, j < i â†’ Term â†’ Prop) : Term â†’ (Term â†’ Prop) â†’ Prop where
  | pi a b Pa (Pf : Term â†’ (Term â†’ Prop) â†’ Prop) :
    Interp i I a Pa â†’
    (âˆ€ x, Pa x â†’ âˆƒ Pb, Pf x Pb) â†’
    (âˆ€ x Pb, Pf x Pb â†’ Interp i I (subst (x +: var) b) Pb) â†’
    Interp i I (pi a b) (Î» f â†¦ âˆ€ x Pb, Pa x â†’ Pf x Pb â†’ Pb (app f x))
  | ğ’° j (lt : j < i) : Interp i I (ğ’° j) (I j lt)
  | â„™ : Interp i I â„™ (âˆƒ Pb, IInterp Â· Pb)
  | step a b P :
    a â‡’ b â†’
    Interp i I b P â†’
    Interp i I a P
notation:40 "âŸ¦" a "âŸ§" i "," I "â†˜" P => Interp i I a P

def Interps (i : lc.L) : Term â†’ (Term â†’ Prop) â†’ Prop :=
  Interp i (Î» j _ a â†¦ âˆƒ P, Interps j a P)
termination_by i

notation:40 "âŸ¦" a "âŸ§" i "â†˜" P => Interps i a P

-- Interpretations of Prop types
inductive Interpâ‚€ : Term â†’ (Term â†’ Prop) â†’ Prop where
  | all i a b Pa (Pf : Term â†’ (Term â†’ Prop) â†’ Prop) :
    Interps i a Pa â†’
    (âˆ€ x, Pa x â†’ âˆƒ Pb, Pf x Pb) â†’
    (âˆ€ x Pb, Pf x Pb â†’ Interpâ‚€ (subst (x +: var) b) Pb) â†’
    Interpâ‚€ (all a b) (Î» f â†¦ âˆ€ x Pb, Pa x â†’ Pf x Pb â†’ Pb (app f x))
  | mty : Interpâ‚€ mty (Î» _ â†¦ False)
  | step a b P : a â‡’ b â†’ Interpâ‚€ b P â†’ Interpâ‚€ a P
notation:40 "âŸ¦" a "âŸ§ â†˜" P => Interpâ‚€ a P

/-*------------------------
  Simple inversion lemmas
------------------------*-/

theorem interpPiInv {i I a b P} (h : âŸ¦ pi a b âŸ§ i , I â†˜ P) :
  âˆƒ (Pa : Term â†’ Prop) (Pf : Term â†’ (Term â†’ Prop) â†’ Prop),
    (âŸ¦ a âŸ§ i , I â†˜ Pa) âˆ§
    (âˆ€ x, Pa x â†’ âˆƒ Pb, Pf x Pb) âˆ§
    (âˆ€ x Pb, Pf x Pb â†’ Interp i I (subst (x +: var) b) Pb) âˆ§
    P = (Î» f â†¦ âˆ€ x Pb, Pa x â†’ Pf x Pb â†’ Pb (app f x)) := by
  generalize e : pi a b = c at h
  induction h generalizing a b <;> try contradiction
  case pi Pa Pf ha hPf hb _ _ =>
    injection e with ea eb; subst ea; subst eb
    exact âŸ¨Pa, Pf, ha, hPf, hb, rflâŸ©
  case step r _ ih =>
    subst e; cases r
    let âŸ¨Pa, Pf, ha, hPf, hb, eâŸ© := ih rfl
    refine âŸ¨Pa, Pf, ?_, hPf, ?_, eâŸ©
    . constructor <;> assumption
    . intro x Pb PfxPb; constructor <;> apply_rules [parCong, parRefl]

theorem interpğ’°Inv {i I j P} (h : âŸ¦ ğ’° j âŸ§ i , I â†˜ P) : âˆƒ lt, P = I j lt := by
  generalize e : ğ’° j = b at h
  induction h generalizing j <;> try contradiction
  case ğ’° j lt => injection e with e; subst e; exists lt
  case step r _ ih => subst e; cases r; exact ih rfl

theorem interpsğ’°Inv {i j P} (h : âŸ¦ ğ’° j âŸ§ i â†˜ P) :
  j < i âˆ§ P = Î» a â†¦ âˆƒ P, âŸ¦ a âŸ§ j â†˜ P := by
  unfold Interps at h
  let âŸ¨lt, eâŸ© := interpğ’°Inv h
  exact âŸ¨lt, eâŸ©

theorem interpâ„™Inv {i I P} (h : âŸ¦ â„™ âŸ§ i , I â†˜ P) : P = (âˆƒ P, IInterp Â· P) := by
  generalize e : â„™ = a at h
  induction h <;> try contradiction
  case â„™ => rfl
  case step r _ ih => subst e; cases r; simp [ih]

theorem interpsâ„™Inv {i P} (h : âŸ¦ â„™ âŸ§ i â†˜ P) : P = (âˆƒ P, IInterp Â· P) := by
  unfold Interps at h; exact interpâ„™Inv h

theorem interpAllInv' {a b P} (h : âŸ¦ all a b âŸ§ â†˜ P) :
  âˆƒ i Pa, âˆƒ (Pf : Term â†’ (Term â†’ Prop) â†’ Prop),
    (âŸ¦ a âŸ§ i â†˜ Pa) âˆ§
    (âˆ€ x, Pa x â†’ âˆƒ Pb, Pf x Pb) âˆ§
    (âˆ€ x Pb, Pf x Pb â†’ Interpâ‚€ (subst (x +: var) b) Pb) âˆ§
    P = (Î» f â†¦ âˆ€ x Pb, Pa x â†’ Pf x Pb â†’ Pb (app f x)) := by
  generalize e : all a b = c at h
  induction h generalizing a b <;> try contradiction
  case all Pa Pf ha hPf hb _ =>
    injection e with ea eb; subst ea; subst eb
    exact âŸ¨_, Pa, Pf, ha, hPf, hb, rflâŸ©
  case step r _ ih =>
    subst e; cases r
    let âŸ¨i, Pa, Pf, ha, hPf, hb, eâŸ© := ih rfl
    refine âŸ¨i, Pa, Pf, ?_, hPf, ?_, eâŸ©
    . unfold Interps at *; constructor <;> assumption
    . intro x Pb PfxPb; constructor <;> apply_rules [parCong, parRefl]

theorem interpMtyInv {P} (h : âŸ¦ mty âŸ§ â†˜ P) : P = Î» _ â†¦ False := by
  generalize e : mty = a at h
  induction h <;> try contradiction
  case mty => rfl
  case step r _ ih => subst e; cases r; simp [ih]

/-*--------------------
  Better constructors
--------------------*-/

theorem interpsPi {i a b Pa P}
  (ha : âŸ¦ a âŸ§ i â†˜ Pa)
  (hb : âˆ€ x, Pa x â†’ âˆƒ Pb, âŸ¦ subst (x +: var) b âŸ§ i â†˜ Pb) :
  P = (Î» f â†¦ âˆ€ x Pb, Pa x â†’ (âŸ¦ subst (x +: var) bâŸ§ i â†˜ Pb) â†’ Pb (app f x)) â†’
  âŸ¦ pi a b âŸ§ i â†˜ P := by
  unfold Interps at *; intro e; subst e; constructor; assumption; assumption; simp

theorem interpsğ’° {i j} (lt : j < i) :
  âŸ¦ ğ’° j âŸ§ i â†˜ (Î» a â†¦ âˆƒ P, âŸ¦ a âŸ§ j â†˜ P) := by
  unfold Interps at *; constructor; assumption

theorem interpsâ„™ {i} : âŸ¦ â„™ âŸ§ i â†˜ (âˆƒ P, IInterp Â· P) := by
  unfold Interps; constructor

theorem interpsAll {i a b Pa P}
  (ha : âŸ¦ a âŸ§ i â†˜ Pa)
  (hb : âˆ€ x, Pa x â†’ âˆƒ Pb, âŸ¦ subst (x +: var) b âŸ§ â†˜ Pb) :
  P = (Î» f â†¦ âˆ€ x Pb, Pa x â†’ (âŸ¦ subst (x +: var) bâŸ§ â†˜ Pb) â†’ Pb (app f x)) â†’
  âŸ¦ all a b âŸ§ â†˜ P := by
  intro e; subst e; constructor; assumption; assumption; simp

/-*------------------------------------------------
  Interpretation respects conversion wrt the type
------------------------------------------------*-/

theorem interpFwd {i I a b P} (r : a â‡’ b) (h : âŸ¦ a âŸ§ i , I â†˜ P) : âŸ¦ b âŸ§ i , I â†˜ P := by
  induction h generalizing b
  case pi iha ihb =>
    cases r; constructor
    all_goals intros; apply_rules [parCong, parRefl]
  case ğ’° => cases r; constructor
  case â„™ => cases r; constructor
  case step r' _ ih =>
    let âŸ¨c, rc, rc'âŸ© := diamond r r'
    constructor <;> apply_rules

theorem interpsFwd {i a b P} (r : a â‡’ b) (h : âŸ¦ a âŸ§ i â†˜ P) : âŸ¦ b âŸ§ i â†˜ P := by
  unfold Interps at *; apply_rules [interpFwd]

theorem interpFwdâ‚€ {a b P} (r : a â‡’ b) (h : âŸ¦ a âŸ§ â†˜ P) : âŸ¦ b âŸ§ â†˜ P := by
  induction h generalizing b
  case all ihb =>
    cases r; constructor
    all_goals intros; apply_rules [interpsFwd, parCong, parRefl]
  case mty => cases r; constructor
  case step r' _ ih =>
    let âŸ¨c, rc, rc'âŸ© := diamond r r'
    constructor <;> apply_rules

theorem interpsFwds {i a b P} (r : a â‡’â‹† b) (h : âŸ¦ a âŸ§ i â†˜ P) : âŸ¦ b âŸ§ i â†˜ P := by
  induction r generalizing P
  all_goals intros; apply_rules [interpsFwd]

theorem interpFwdsâ‚€ {a b P} (r : a â‡’â‹† b) (h : âŸ¦ a âŸ§ â†˜ P) : âŸ¦ b âŸ§ â†˜ P := by
  induction r generalizing P
  all_goals intros; apply_rules [interpFwdâ‚€]

theorem interpsBwd {i a b P} (r : a â‡’ b) (h : âŸ¦ b âŸ§ i â†˜ P) : âŸ¦ a âŸ§ i â†˜ P := by
  unfold Interps at *; constructor <;> assumption

theorem interpsBwds {i a b P} (r : a â‡’â‹† b) (h : âŸ¦ b âŸ§ i â†˜ P) : âŸ¦ a âŸ§ i â†˜ P := by
  induction r generalizing P <;> apply_rules [interpsBwd]

theorem interpBwdsâ‚€ {a b P} (r : a â‡’â‹† b) (h : âŸ¦ b âŸ§ â†˜ P) : âŸ¦ a âŸ§ â†˜ P := by
  induction r generalizing P <;> apply_rules [Interpâ‚€.step]

theorem interpsConv {i a b P} (r : a â‡” b) (h : âŸ¦ a âŸ§ i â†˜ P) : âŸ¦ b âŸ§ i â†˜ P :=
  let âŸ¨_, ra, rbâŸ© := r
  interpsBwds rb (interpsFwds ra h)

theorem interpConvâ‚€ {a b P} (r : a â‡” b) (h : âŸ¦ a âŸ§ â†˜ P) : âŸ¦ b âŸ§ â†˜ P :=
  let âŸ¨_, ra, rbâŸ© := r
  interpBwdsâ‚€ rb (interpFwdsâ‚€ ra h)

/-*----------------------------------------------------
  Backward preservation of interpretation predicate
  (we don't need the forward direction or conversion)
----------------------------------------------------*-/

theorem interpsBwdsP {i a x y P} (r : x â‡’â‹† y) (h : âŸ¦ a âŸ§ i â†˜ P) : P y â†’ P x := by
  unfold Interps at h; induction h generalizing x y
  case pi ihb =>
    intro h x Pb Pax PfxPb
    exact ihb x Pb PfxPb (parsApp r (Pars.refl x)) (h x Pb Pax PfxPb)
  case ğ’° => exact Î» âŸ¨P, hâŸ© â†¦ âŸ¨P, interpsBwds r hâŸ©
  case â„™ =>
    intro âŸ¨P, I, hI, bwdâŸ©
    exact âŸ¨P, I, bwd r hI, bwdâŸ©
  case step ih => exact ih r

theorem interpBwdsPâ‚€ {a x y P} (r : x â‡’â‹† y) (h : âŸ¦ a âŸ§ â†˜ P) : P y â†’ P x := by
  induction h generalizing x y
  case all ihb =>
    intro h x Pb Pax PfxPb
    exact ihb x Pb PfxPb (parsApp r (Pars.refl x)) (h x Pb Pax PfxPb)
  case mty => simp
  case step ih => exact ih r

/-*--------------------------------
  Interpretations are propInterps
--------------------------------*-/

theorem interpsIInterp {i a P} (h : âŸ¦ a âŸ§ i â†˜ P) : IInterp a P :=
  âŸ¨Î» a P â†¦ âŸ¦ a âŸ§ i â†˜ P, h, interpsBwdsâŸ©

theorem interpIInterpâ‚€ {a P} (h : âŸ¦ a âŸ§ â†˜ P) : IInterp a P :=
  âŸ¨Î» a P â†¦ âŸ¦ a âŸ§ â†˜ P, h, interpBwdsâ‚€âŸ©

/-*--------------------------------
  Interpretation is deterministic
--------------------------------*-/

-- âš ï¸ uses funext and propext âš ï¸
theorem interpDet' {i I a P Q} (hP : âŸ¦ a âŸ§ i , I â†˜ P) (hQ : âŸ¦ a âŸ§ i , I â†˜ Q) : P = Q := by
  induction hP generalizing Q
  case pi Pa Pf _ hPf _ iha ihb =>
    let âŸ¨Pa', Pf', ha', hPf', hb', eâŸ© := interpPiInv hQ
    subst e; apply funext; intro f
    apply propext; constructor
    . intro h x Pb' Pax' PfxPb'
      have Pax : Pa x := by rw [iha ha']; exact Pax'
      let âŸ¨Pb, PfxPbâŸ© := hPf x Pax
      rw [â† ihb x Pb PfxPb (hb' x Pb' PfxPb')]
      exact h x Pb Pax PfxPb
    . intro h x Pb Pax PfxPb
      have Pax' : Pa' x := by rw [â† iha ha']; exact Pax
      let âŸ¨Pb', PfxPb'âŸ© := hPf' x Pax'
      rw [ihb x Pb PfxPb (hb' x Pb' PfxPb')]
      exact h x Pb' Pax' PfxPb'
  case ğ’° => let âŸ¨_, eâŸ© := interpğ’°Inv hQ; simp [e]
  case â„™ => simp [interpâ„™Inv hQ]
  case step r _ ih => exact ih (interpFwd r hQ)

theorem interpsDet' {i a P Q} (hP : âŸ¦ a âŸ§ i â†˜ P) (hQ : âŸ¦ a âŸ§ i â†˜ Q) : P = Q := by
  unfold Interps at *; apply_rules [interpDet']

theorem interpsCumul {i j a P} (lt : i < j) (h : âŸ¦ a âŸ§ i â†˜ P) : âŸ¦ a âŸ§ j â†˜ P := by
  unfold Interps at h; induction h generalizing j <;> unfold Interps
  case pi iha ihb =>
    constructor
    . unfold Interps at iha; exact iha lt
    . assumption
    . intro x Pb PfxPb; unfold Interps at ihb; exact ihb x Pb PfxPb lt
  case ğ’° k _ => constructor; apply_rules [IsTrans.trans]
  case â„™ => constructor
  case step ih => constructor; assumption; unfold Interps at ih; exact ih lt

-- âš ï¸ uses trichotomy of < âš ï¸
theorem interpsDet {i j a P Q} (hP : âŸ¦ a âŸ§ i â†˜ P) (hQ : âŸ¦ a âŸ§ j â†˜ Q) : P = Q := by
  rcases trichotomous (r := lc.lt.lt) i j with lt | eq | gt
  . exact interpsDet' (interpsCumul lt hP) hQ
  . rw [eq] at hP; apply interpsDet' hP hQ
  . exact interpsDet' hP (interpsCumul gt hQ)

-- âš ï¸ uses funext and propext âš ï¸
theorem interpDetâ‚€ {a P Q} (hP : âŸ¦ a âŸ§ â†˜ P) (hQ : âŸ¦ a âŸ§ â†˜ Q) : P = Q := by
  induction hP generalizing Q
  case all Pa Pf ha hPf _ ihb =>
    let âŸ¨i, Pa', Pf', ha', hPf', hb', eâŸ© := interpAllInv' hQ
    subst e; apply funext; intro f
    apply propext; constructor
    . intro h x Pb' Pax' PfxPb'
      have Pax : Pa x := by rw [interpsDet ha ha']; exact Pax'
      let âŸ¨Pb, PfxPbâŸ© := hPf x Pax
      rw [â† ihb x Pb PfxPb (hb' x Pb' PfxPb')]
      exact h x Pb Pax PfxPb
    . intro h x Pb Pax PfxPb
      have Pax' : Pa' x := by rw [interpsDet ha' ha]; exact Pax
      let âŸ¨Pb', PfxPb'âŸ© := hPf' x Pax'
      rw [ihb x Pb PfxPb (hb' x Pb' PfxPb')]
      exact h x Pb' Pax' PfxPb'
  case mty => simp [interpMtyInv hQ]
  case step r _ ih => exact ih (interpFwdâ‚€ r hQ)

/-*------------------------
  Better inversion lemmas
------------------------*-/

-- âš ï¸ uses funext and propext âš ï¸
theorem interpPiInv' {i I a b P} (h : âŸ¦ pi a b âŸ§ i , I â†˜ P) :
  âˆƒ Pa, (âŸ¦ a âŸ§ i , I â†˜ Pa) âˆ§
    (âˆ€ x, Pa x â†’ âˆƒ Pb, âŸ¦ subst (x +: var) b âŸ§ i , I â†˜ Pb) âˆ§
    P = (Î» f â†¦ âˆ€ x Pb, Pa x â†’ (âŸ¦ subst (x +: var) bâŸ§ i , I â†˜ Pb) â†’ Pb (app f x)) := by
  let âŸ¨Pa, Pf, ha, hPf, hfb, eâŸ© := interpPiInv h
  refine âŸ¨Pa, ha, ?_, ?_âŸ©
  . intro x Pax; let âŸ¨Pb, PfxPbâŸ© := hPf x Pax
    exact âŸ¨Pb, hfb x Pb PfxPbâŸ©
  . subst e; apply funext; intro f; apply propext; constructor
    . intro h x Pb Pax hb
      let âŸ¨Pb', PfxPb'âŸ© := hPf x Pax
      have e : Pb = Pb' := by apply interpDet' hb (hfb x Pb' PfxPb')
      rw [e]; apply_rules
    . intros; apply_rules

theorem interpsPiInv {i a b P} (h : âŸ¦ pi a b âŸ§ i â†˜ P) :
  âˆƒ Pa, (âŸ¦ a âŸ§ i â†˜ Pa) âˆ§
    (âˆ€ x, Pa x â†’ âˆƒ Pb, âŸ¦ subst (x +: var) b âŸ§ i â†˜ Pb) âˆ§
    P = (Î» f â†¦ âˆ€ x Pb, Pa x â†’ (âŸ¦ subst (x +: var) bâŸ§ i â†˜ Pb) â†’ Pb (app f x)) := by
  unfold Interps at *; exact interpPiInv' h

-- âš ï¸ uses funext and propext âš ï¸
theorem interpAllInv {a b P} (h : âŸ¦ all a b âŸ§ â†˜ P) :
  âˆƒ i Pa, (âŸ¦ a âŸ§ i â†˜ Pa) âˆ§
    (âˆ€ x, Pa x â†’ âˆƒ Pb, âŸ¦ subst (x +: var) b âŸ§ â†˜ Pb) âˆ§
    P = (Î» f â†¦ âˆ€ x Pb, Pa x â†’ (âŸ¦ subst (x +: var) bâŸ§ â†˜ Pb) â†’ Pb (app f x)) := by
  let âŸ¨i, Pa, Pf, ha, hPf, hfb, eâŸ© := interpAllInv' h
  refine âŸ¨i, Pa, ha, ?_, ?_âŸ©
  . intro x Pax; let âŸ¨Pb, PfxPbâŸ© := hPf x Pax
    exact âŸ¨Pb, hfb x Pb PfxPbâŸ©
  . subst e; apply funext; intro f; apply propext; constructor
    . intro h x Pb Pax hb
      let âŸ¨Pb', PfxPb'âŸ© := hPf x Pax
      have e : Pb = Pb' := by apply interpDetâ‚€ hb (hfb x Pb' PfxPb')
      rw [e]; apply_rules
    . intros; apply_rules

/-*----------------
  Semantic typing
----------------*-/

open sort

def semSubst Ïƒ Î“ := âˆ€ x a,
  (In ğ’° x a Î“ â†’ âˆƒ i P, (âŸ¦ subst Ïƒ a âŸ§ i â†˜ P) âˆ§ P (Ïƒ x)) âˆ§
  (In â„™ x a Î“ â†’ âˆƒ P, (âŸ¦ subst Ïƒ a âŸ§ â†˜ P) âˆ§ P (Ïƒ x))
infix:40 "âŠ¨" => semSubst

def semWtğ’° Î“ a A := âˆ€ Ïƒ, Ïƒ âŠ¨ Î“ â†’ âˆƒ i P, (âŸ¦ subst Ïƒ A âŸ§ i â†˜ P) âˆ§ P (subst Ïƒ a)
notation:40 Î“:41 "âŠ¨" a:41 "âˆ¶" A:41 => semWtğ’° Î“ a A

def semWtâ„™ Î“ a A := âˆ€ Ïƒ, Ïƒ âŠ¨ Î“ â†’ âˆƒ P, (âŸ¦ subst Ïƒ A âŸ§ â†˜ P) âˆ§ P (subst Ïƒ a)
notation:40 Î“:41 "âŠ¨â‚š" a:41 "âˆ¶" A:41 => semWtâ„™ Î“ a A

theorem semSubstNil Ïƒ : Ïƒ âŠ¨ â¬ := by
  intros _ _; constructor
  all_goals intro mem; cases mem

theorem semSubstCons {Î“ : Ctxt} {Ïƒ i a A P} :
  (âŸ¦ subst Ïƒ A âŸ§ i â†˜ P) â†’ P a â†’
  Ïƒ âŠ¨ Î“ â†’ a +: Ïƒ âŠ¨ Î“ âˆ· A := by
  intro hA ha hÏƒ x B; constructor <;> intro mem
  . cases mem
    case here => rw [substRenamed]; exists i, P
    case there x B mem =>
      rw [substRenamed]
      let âŸ¨inğ’°, _âŸ© := hÏƒ x B
      exact inğ’° mem
  . cases mem
    case there x B mem =>
      rw [substRenamed]
      let âŸ¨_, inâ„™âŸ© := hÏƒ x B
      exact inâ„™ mem

theorem semSubstICons {Î“ : Ctxt} {Ïƒ a A P} :
  (âŸ¦ subst Ïƒ A âŸ§ â†˜ P) â†’ P a â†’
  Ïƒ âŠ¨ Î“ â†’ a +: Ïƒ âŠ¨ Î“ âˆ·áµ¢ A := by
  intro hA ha hÏƒ x B; constructor <;> intro mem
  . cases mem
    case ithere x B mem =>
      rw [substRenamed]
      let âŸ¨inğ’°, _âŸ© := hÏƒ x B
      exact inğ’° mem
  . cases mem
    case ihere => rw [substRenamed]; exact âŸ¨P, hA, haâŸ©
    case ithere x B mem =>
      rw [substRenamed]
      let âŸ¨_, inâ„™âŸ© := hÏƒ x B
      exact inâ„™ mem
