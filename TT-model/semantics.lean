import Â«TT-modelÂ».syntactics
import Â«TT-modelÂ».reduction

open LevelClass (L)
open Term

set_option autoImplicit false

variable [lc : LevelClass]

inductive Interp i (I : (j : L) â†’ j < i â†’ Term â†’ Prop) : Term â†’ (Term â†’ Prop) â†’ Prop where
  | pi a b Pa (Pf : Term â†’ (Term â†’ Prop) â†’ Prop) :
    Interp i I a Pa â†’
    (âˆ€ x, Pa x â†’ âˆƒ Pb, Pf x Pb) â†’
    (âˆ€ x Pb, Pf x Pb â†’ Interp i I (subst (x +: var) b) Pb) â†’
    Interp i I (pi a b) (Î» f â†¦ âˆ€ x Pb, Pa x â†’ Pf x Pb â†’ Pb (app f x))
  | ğ’° (j : L) (lt : j < i) : Interp i I (ğ’° (lof j)) (I j lt)
  | mty : Interp i I mty (Î» _ â†¦ False)
  | lvl k : Interp i I (lvl (lof k)) (Î» a â†¦ âˆƒ j, a â‡’â‹† lof j âˆ§ j < k)
  | step a b P :
    a â‡’ b â†’
    Interp i I b P â†’
    Interp i I a P
notation:40 "âŸ¦" a "âŸ§" i "," I "â†˜" P => Interp i I a P

def Interps (i : L) : Term â†’ (Term â†’ Prop) â†’ Prop :=
  Interp i (Î» j _ a â†¦ âˆƒ P, Interps j a P)
termination_by i

notation:40 "âŸ¦" a "âŸ§" i "â†˜" P => Interps i a P

/-*------------------------
  Simple inversion lemmas
------------------------*-/

theorem interpPiInv {i I a b P} (h : âŸ¦ pi a b âŸ§ i , I â†˜ P) :
  âˆƒ (Pa : Term â†’ Prop) (Pf : Term â†’ (Term â†’ Prop) â†’ Prop),
    (âŸ¦ a âŸ§ i , I â†˜ Pa) âˆ§
    (âˆ€ x, Pa x â†’ âˆƒ Pb, Pf x Pb) âˆ§
    (âˆ€ x Pb, Pf x Pb â†’ Interp i I (subst (x +: var) b) Pb) âˆ§
    P = (Î» f â†¦ âˆ€ x Pb, Pa x â†’ Pf x Pb â†’ Pb (app f x)) := by
  generalize e : pi a b = c at h
  revert a b e; induction h <;> intro a b e
  case pi Pa Pf ha hPf hb _ _ =>
    injection e with ea eb; subst ea; subst eb
    exact âŸ¨Pa, Pf, ha, hPf, hb, rflâŸ©
  case ğ’° => contradiction
  case mty => contradiction
  case lvl => contradiction
  case step r _ ih =>
    subst e; cases r
    match ih rfl with
    | âŸ¨Pa, Pf, ha, hPf, hb, eâŸ© =>
      refine âŸ¨Pa, Pf, ?_, hPf, ?_, eâŸ©
      . constructor <;> assumption
      . intro x Pb PfxPb; constructor
        . apply parCong; apply parRefl; assumption
        . exact hb x Pb PfxPb

theorem interpğ’°Inv {i I a P} (h : âŸ¦ ğ’° a âŸ§ i , I â†˜ P) : âˆƒ j lt, a â‡’â‹† lof j âˆ§ P = I j lt := by
  generalize e : ğ’° a = b at h
  revert a; induction h
  all_goals intro a e; try contradiction
  case ğ’° j lt => injection e with e; subst e; exists j, lt, Pars.refl _
  case step r _ ih => subst e; cases r; case ğ’° r _ =>
    match ih rfl with
    | âŸ¨j, lt, r, eâŸ© => refine âŸ¨j, lt, ?_, eâŸ©; constructor <;> assumption

theorem interpMtyInv {i I P} (h : âŸ¦ mty âŸ§ i , I â†˜ P) : P = (Î» _ â†¦ False) := by
  generalize e : mty = a at h
  revert e; induction h
  all_goals intro e; try contradiction
  case mty => rfl
  case step r _ ih => subst e; cases r; simp [ih]

theorem interpLvlInv {i I a P} (h : âŸ¦ lvl a âŸ§ i , I â†˜ P) : âˆƒ k, a â‡’â‹† lof k âˆ§ P = (Î» a â†¦ âˆƒ j, a â‡’â‹† lof j âˆ§ j < k) := by
  generalize e : lvl a = b at h
  revert a; induction h
  all_goals intro a e; try contradiction
  case lvl k => injection e with e; subst e; exists k, Pars.refl _
  case step r _ ih => subst e; cases r; case lvl r _ =>
    match ih rfl with
    | âŸ¨k, r, eâŸ© => refine âŸ¨k, ?_, eâŸ©; constructor <;> assumption

/-*--------------------
  Better constructors
--------------------*-/

theorem interpsPi {i a b Pa P}
  (ha : âŸ¦ a âŸ§ i â†˜ Pa)
  (hb : âˆ€ x, Pa x â†’ âˆƒ Pb, âŸ¦ subst (x +: var) b âŸ§ i â†˜ Pb) :
  P = (Î» f â†¦ âˆ€ x Pb, Pa x â†’ (âŸ¦ subst (x +: var) bâŸ§ i â†˜ Pb) â†’ Pb (app f x)) â†’
  âŸ¦ pi a b âŸ§ i â†˜ P := by
  unfold Interps at *; intro e; subst e; constructor; assumption; assumption; simp

theorem interpsğ’° {i j : L} (lt : j < i) :
  âŸ¦ ğ’° (lof j) âŸ§ i â†˜ (Î» a â†¦ âˆƒ P, âŸ¦ a âŸ§ j â†˜ P) := by
  unfold Interps at *; constructor; assumption

theorem interpsMty {i} : âŸ¦ mty âŸ§ i â†˜ (Î» _ â†¦ False) := by
  unfold Interps at *; constructor

theorem interpsLvl {i k} : âŸ¦ lvl (lof k) âŸ§ i â†˜ (Î» a â†¦ âˆƒ j, a â‡’â‹† lof j âˆ§ j < k) := by
  unfold Interps at *; constructor

/-*------------------------------------------------
  Interpretation respects conversion wrt the type
------------------------------------------------*-/

theorem interpFwd {i I a b P} (r : a â‡’ b) (h : âŸ¦ a âŸ§ i , I â†˜ P) : âŸ¦ b âŸ§ i , I â†˜ P := by
  revert b; induction h <;> intro b r
  case pi iha ihb =>
    cases r; constructor
    . apply iha; assumption
    . assumption
    . intros; apply ihb; assumption; apply parCong; apply parRefl; assumption
  case ğ’° => cases r; case ğ’° r => cases r; constructor
  case mty => cases r; apply Interp.step <;> constructor
  case lvl => cases r; case lvl r => cases r; constructor
  case step r' _ ih =>
    match diamond r r' with
    | âŸ¨c, rc, rc'âŸ© => constructor; exact rc; exact (ih rc')

theorem interpsFwd {i a b P} (r : a â‡’ b) (h : âŸ¦ a âŸ§ i â†˜ P) : âŸ¦ b âŸ§ i â†˜ P := by
  unfold Interps at *; apply interpFwd; exact r; assumption

theorem interpsBwd {i a b P} (r : a â‡’ b) (h : âŸ¦ b âŸ§ i â†˜ P) : âŸ¦ a âŸ§ i â†˜ P := by
  unfold Interps at *; constructor; exact r; assumption

theorem interpsFwds {i a b P} (r : a â‡’â‹† b) (h : âŸ¦ a âŸ§ i â†˜ P) : âŸ¦ b âŸ§ i â†˜ P := by
  revert P; induction r
  case refl => simp
  case trans ih => intro P h; apply ih; apply interpsFwd <;> assumption

theorem interpsBwds {i a b P} (r : a â‡’â‹† b) (h : âŸ¦ b âŸ§ i â†˜ P) : âŸ¦ a âŸ§ i â†˜ P := by
  revert P; induction r
  case refl => simp
  case trans ih => intro P h; apply interpsBwd; assumption; apply ih; assumption

theorem interpsConv {i a b P} (r : a â‡” b) (h : âŸ¦ a âŸ§ i â†˜ P) : âŸ¦ b âŸ§ i â†˜ P :=
  match r with
  | âŸ¨_, ra, rbâŸ© => interpsBwds rb (interpsFwds ra h)

/-*----------------------------------------------------
  Backward preservation of interpretation predicate
  (we don't need the forward direction or conversion)
----------------------------------------------------*-/

theorem interpsBwdsP {i a x y P} (r : x â‡’â‹† y) (h : âŸ¦ a âŸ§ i â†˜ P) : P y â†’ P x := by
  revert x y; unfold Interps at h; induction h
  case pi ihb =>
    intro _ _ r h x Pb Pax PfxPb
    exact ihb x Pb PfxPb (parsApp r (Pars.refl x)) (h x Pb Pax PfxPb)
  case ğ’° => exact Î» r âŸ¨P, hâŸ© â†¦ âŸ¨P, interpsBwds r hâŸ©
  case mty => simp
  case lvl =>
    intro _ _ _ âŸ¨j, _, ltâŸ©
    refine âŸ¨j, ?_, ltâŸ©
    apply parsTrans <;> assumption
  case step ih => exact ih

/-*--------------------------------
  Interpretation is deterministic
--------------------------------*-/

-- âš ï¸ uses funext and propext âš ï¸
theorem interpDet' {i I a P Q} (hP : âŸ¦ a âŸ§ i , I â†˜ P) (hQ : âŸ¦ a âŸ§ i , I â†˜ Q) : P = Q := by
  revert Q; induction hP <;> intro Q hQ
  case pi Pa Pf _ hPf _ iha ihb =>
    match interpPiInv hQ with
    | âŸ¨Pa', Pf', ha', hPf', hb', eâŸ© =>
      subst e; apply funext; intro f
      apply propext; constructor
      . intro h x Pb' Pax' PfxPb'
        have Pax : Pa x := by rw [iha ha']; exact Pax'
        match hPf x Pax with
        | âŸ¨Pb, PfxPbâŸ© =>
          rw [â† ihb x Pb PfxPb (hb' x Pb' PfxPb')]
          exact h x Pb Pax PfxPb
      . intro h x Pb Pax PfxPb
        have Pax' : Pa' x := by rw [â† iha ha']; exact Pax
        match hPf' x Pax' with
        | âŸ¨Pb', PfxPb'âŸ© =>
          rw [ihb x Pb PfxPb (hb' x Pb' PfxPb')]
          exact h x Pb' Pax' PfxPb'
  case ğ’° =>
    match interpğ’°Inv hQ with
    | âŸ¨j, _, r, eâŸ© => injection (parsLofInv r) with ej; subst ej; simp [e]
  case mty => simp [interpMtyInv hQ]
  case lvl =>
    match interpLvlInv hQ with
    | âŸ¨k, r, eâŸ© => injection (parsLofInv r) with ek; subst ek; simp [e]
  case step r _ ih => exact ih (interpFwd r hQ)

theorem interpsDet' {i a P Q} (hP : âŸ¦ a âŸ§ i â†˜ P) (hQ : âŸ¦ a âŸ§ i â†˜ Q) : P = Q := by
  unfold Interps at *; apply interpDet' <;> assumption

theorem interpsCumul {i j : L} {a P} (lt : i < j) (h : âŸ¦ a âŸ§ i â†˜ P) : âŸ¦ a âŸ§ j â†˜ P := by
  revert j; unfold Interps at h; induction h
  all_goals intro j lt; unfold Interps
  case pi iha ihb =>
    constructor
    . unfold Interps at iha; exact iha lt
    . assumption
    . intros; unfold Interps at ihb; apply ihb <;> assumption
  case ğ’° k _ => constructor; apply IsTrans.trans <;> assumption
  case mty => constructor
  case lvl => constructor
  case step ih => constructor; assumption; unfold Interps at ih; apply ih lt

-- this is the only place we need trichotomy of <
theorem interpsDet {i j a P Q} (hP : âŸ¦ a âŸ§ i â†˜ P) (hQ : âŸ¦ a âŸ§ j â†˜ Q) : P = Q := by
  rcases IsTrichotomous.trichotomous (lt := lc.lt) i j with lt | eq | gt
  . apply interpsDet' _ hQ; apply interpsCumul lt hP
  . rw [eq] at hP; apply interpsDet' hP hQ
  . apply interpsDet' hP; apply interpsCumul gt hQ

/-*------------------------
  Better inversion lemmas
------------------------*-/

-- âš ï¸ uses funext and propext âš ï¸
theorem interpPiInv' {i I a b P} (h : âŸ¦ pi a b âŸ§ i , I â†˜ P) :
  âˆƒ Pa, (âŸ¦ a âŸ§ i , I â†˜ Pa) âˆ§
    (âˆ€ x, Pa x â†’ âˆƒ Pb, âŸ¦ subst (x +: var) b âŸ§ i , I â†˜ Pb) âˆ§
    P = Î» f â†¦ âˆ€ x Pb, Pa x â†’ (âŸ¦ subst (x +: var) bâŸ§ i , I â†˜ Pb) â†’ Pb (app f x) := by
  match interpPiInv h with
  | âŸ¨Pa, Pf, ha, hPf, hfb, eâŸ© =>
    refine âŸ¨Pa, ha, ?_, ?_âŸ©
    . intro x Pax; match hPf x Pax with
      | âŸ¨Pb, PfxPbâŸ© => exact âŸ¨Pb, hfb x Pb PfxPbâŸ©
    . subst e; apply funext; intro f; apply propext; constructor
      . intro h x Pb Pax hb
        apply h x Pb Pax
        match hPf x Pax with
        | âŸ¨Pb', PfxPb'âŸ© =>
        have e : Pb = Pb' := by apply interpDet' hb (hfb x Pb' PfxPb')
        rw [e]; exact PfxPb'
      . intro h x Pb Pax PfxPb
        apply h x Pb Pax
        apply hfb x Pb PfxPb

theorem interpsPiInv {i a b P} (h : âŸ¦ pi a b âŸ§ i â†˜ P) :
  âˆƒ Pa, (âŸ¦ a âŸ§ i â†˜ Pa) âˆ§
    (âˆ€ x, Pa x â†’ âˆƒ Pb, âŸ¦ subst (x +: var) b âŸ§ i â†˜ Pb) âˆ§
    P = Î» f â†¦ âˆ€ x Pb, Pa x â†’ (âŸ¦ subst (x +: var) bâŸ§ i â†˜ Pb) â†’ Pb (app f x) := by
  unfold Interps at *; apply interpPiInv' h

theorem interpsğ’°Inv {i a P} (h : âŸ¦ ğ’° a âŸ§ i â†˜ P) :
  âˆƒ j, a â‡’â‹† lof j âˆ§ j < i âˆ§ P = Î» a â†¦ âˆƒ P, âŸ¦ a âŸ§ j â†˜ P := by
  unfold Interps at h
  match interpğ’°Inv h with
  | âŸ¨j, lt, r, eâŸ© => exact âŸ¨j, r, lt, eâŸ©

theorem interpsMtyInv {i P} (h : âŸ¦ mty âŸ§ i â†˜ P) : P = (Î» _ â†¦ False) := by
  unfold Interps at h; apply interpMtyInv h

theorem interpsLvlInv {i a P} (h : âŸ¦ lvl a âŸ§ i â†˜ P) : âˆƒ k, a â‡’â‹† lof k âˆ§ P = (Î» a â†¦ âˆƒ j, a â‡’â‹† lof j âˆ§ j < k) := by
  unfold Interps at h; apply interpLvlInv h

/-*----------------
  Semantic typing
----------------*-/

def semSubst Ïƒ Î“ := âˆ€ x a, In x a Î“ â†’ âˆƒ i P, (âŸ¦ subst Ïƒ a âŸ§ i â†˜ P) âˆ§ P (Ïƒ x)
infix:40 "âŠ¨" => semSubst

def semWt Î“ a A := âˆ€ Ïƒ, Ïƒ âŠ¨ Î“ â†’ âˆƒ i P, (âŸ¦ subst Ïƒ A âŸ§ i â†˜ P) âˆ§ P (subst Ïƒ a)
notation:40 Î“:41 "âŠ¨" a:41 "âˆ¶" A:41 => semWt Î“ a A

theorem semSubstNil Ïƒ : Ïƒ âŠ¨ â¬ := by
  intro _ _ mem; cases mem

theorem semSubstCons {Î“ : Ctxt} {Ïƒ i a A P} :
  (âŸ¦ subst Ïƒ A âŸ§ i â†˜ P) â†’ P a â†’
  Ïƒ âŠ¨ Î“ â†’ a +: Ïƒ âŠ¨ Î“ âˆ· A := by
  intro hA ha hÏƒ x B mem
  cases mem
  case here => rw [substRenamed]; exists i, P
  case there B mem => rw [substRenamed]; apply hÏƒ; assumption
