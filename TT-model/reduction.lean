import Â«TT-modelÂ».syntactics

open Term

set_option autoImplicit false
set_option pp.fieldNotation false

variable [LevelClass]

/-*-------------------
  Parallel reduction
-------------------*-/

section
set_option hygiene false
local infix:40 "â‡’" => Par

inductive Par : Term â†’ Term â†’ Prop where
  | Î² {b b' a a'} :
    b â‡’ b' â†’
    a â‡’ a' â†’
    ------------------------------------
    app (abs b) a â‡’ subst (a' +: var) b'
  | var s : var s â‡’ var s
  | ğ’° {k} : ğ’° k â‡’ ğ’° k
  | â„™ : â„™ â‡’ â„™
  | pi {a a' b b'} :
    a â‡’ a' â†’
    b â‡’ b' â†’
    -----------------
    pi a b â‡’ pi a' b'
  | all {a a' b b'} :
    a â‡’ a' â†’
    b â‡’ b' â†’
    -----------------
    all a b â‡’ all a' b'
  | abs {b b'} :
    b â‡’ b' â†’
    --------------
    abs b â‡’ abs b'
  | app {b b' a a'} :
    b â‡’ b' â†’
    a â‡’ a' â†’
    -------------------
    app b a â‡’ app b' a'
  | mty : mty â‡’ mty
  | exf {b b'} :
    b â‡’ b' â†’
    --------------
    exf b â‡’ exf b'
end

infix:40 "â‡’" => Par

theorem parRefl a : a â‡’ a := by
  induction a <;> constructor <;> assumption

theorem parRename {a b} Î¾ (r : a â‡’ b) : rename Î¾ a â‡’ rename Î¾ b := by
  induction r generalizing Î¾ <;> try constructor
  case Î² ihb iha => rw [â† renameDist]; constructor; apply ihb; apply iha
  all_goals apply_assumption

theorem parLift Ïƒ Ï„ (h : âˆ€ x, Ïƒ x â‡’ Ï„ x) : âˆ€ x, (â‡‘ Ïƒ) x â‡’ (â‡‘ Ï„) x := by
  intro n; cases n
  case zero => constructor
  case succ n => apply parRename; apply h

theorem parMorphing {a b} Ïƒ Ï„ (h : âˆ€ x, Ïƒ x â‡’ Ï„ x) (r : a â‡’ b) : subst Ïƒ a â‡’ subst Ï„ b := by
  induction r generalizing Ïƒ Ï„ h <;> try constructor
  case Î² ihb iha =>
    rw [â† substDist]; constructor
    all_goals apply_rules [parLift]
  all_goals apply_rules [parLift]

theorem parSubst {a b} Ïƒ (r : a â‡’ b) : subst Ïƒ a â‡’ subst Ïƒ b := by
  apply parMorphing (r := r); intros; apply parRefl

theorem parCong {a a' b b'} (ra : a â‡’ a') (rb : b â‡’ b') : subst (a +: var) b â‡’ subst (a' +: var) b' := by
  apply parMorphing (r := rb); intro n; cases n <;> first | assumption | constructor

/-*----------------------------------------------------
  Reflexive, transitive closure of parallel reduction
----------------------------------------------------*-/

section
set_option hygiene false
local infix:40 "â‡’â‹†" => Pars

inductive Pars : Term â†’ Term â†’ Prop where
  | refl a : a â‡’â‹† a
  | trans {a b c} : a â‡’ b â†’ b â‡’â‹† c â†’ a â‡’â‹† c
end

infix:40 "â‡’â‹†" => Pars
open Pars

theorem parPars {a b} (r : a â‡’ b) : a â‡’â‹† b := by
  constructor; assumption; constructor

theorem parsTrans {a b c} (râ‚ : a â‡’â‹† b) (râ‚‚ : b â‡’â‹† c) : a â‡’â‹† c := by
  induction râ‚
  case refl => assumption
  case trans ih => constructor <;> apply_rules

theorem parsRename {a b} Î¾ (r : a â‡’â‹† b) : rename Î¾ a â‡’â‹† rename Î¾ b := by
  induction r <;> constructor
  all_goals apply_rules [parRename]

theorem parsSubst {a b} Ïƒ (r : a â‡’â‹† b) : subst Ïƒ a â‡’â‹† subst Ïƒ b := by
  induction r <;> constructor
  all_goals apply_rules [parSubst]

theorem parsCong {a a' b b'} (ra : a â‡’â‹† a') (rb : b â‡’â‹† b') : subst (a +: var) b â‡’â‹† subst (a' +: var) b' := by
  induction ra generalizing rb
  case refl => apply_rules [parsSubst]
  case trans ih => constructor <;> apply_rules [parCong, parRefl]

/-*------------------------------------------
  Constructors for parallel multi-reduction
------------------------------------------*-/

theorem parsPi {a a' b b'} (ra : a â‡’â‹† a') (rb : b â‡’â‹† b') : pi a b â‡’â‹† pi a' b' := by
  induction ra generalizing b b' <;> induction rb
  case refl.refl => constructor
  case refl.trans ih =>
    constructor; constructor; apply parRefl; assumption; apply ih
  case trans.refl ih _ =>
    constructor; constructor; assumption; apply parRefl; apply ih; constructor
  case trans.trans ih _ _ _ _ _ _ =>
    constructor; constructor; assumption; assumption; apply ih; assumption

theorem parsAll {a a' b b'} (ra : a â‡’â‹† a') (rb : b â‡’â‹† b') : all a b â‡’â‹† all a' b' := by
  revert b b' rb; induction ra <;> intro b b' rb <;> induction rb
  case refl.refl => constructor
  case refl.trans ih =>
    constructor; constructor; apply parRefl; assumption; apply ih
  case trans.refl ih _ =>
    constructor; constructor; assumption; apply parRefl; apply ih; constructor
  case trans.trans ih _ _ _ _ _ _ =>
    constructor; constructor; assumption; assumption; apply ih; assumption

theorem parsAbs {b b'} (r : b â‡’â‹† b') : abs b â‡’â‹† abs b' := by
  induction r
  case refl => constructor
  case trans => constructor; constructor; assumption; assumption

theorem parsApp {a a' b b'} (rb : b â‡’â‹† b') (ra : a â‡’â‹† a') : app b a â‡’â‹† app b' a' := by
  induction rb generalizing a a' ra <;> induction ra
  case refl => constructor
  case refl.trans =>
    constructor; constructor; apply parRefl; assumption; assumption
  case trans.refl ih _ =>
    constructor; constructor; assumption; apply parRefl; apply ih; constructor
  case trans.trans ih _ _ _ _ _ _ =>
    constructor; constructor; assumption; assumption; apply ih; assumption

theorem parsExf {b b'} (r : b â‡’â‹† b') : exf b â‡’â‹† exf b' := by
  induction r
  case refl => constructor
  case trans => constructor; constructor; assumption; assumption

theorem parsÎ² Ïƒ b a : app (abs (subst (â‡‘ Ïƒ) b)) a â‡’â‹† subst (a +: Ïƒ) b := by
  constructor
  . constructor; apply parRefl; apply parRefl
  . rw [â† substUnion]; constructor

/-*--------------------------------------------------
  Inversion principles for parallel multi-reduction
--------------------------------------------------*-/

theorem parsğ’°Inv {k b} (r : ğ’° k â‡’â‹† b) : b = ğ’° k := by
  generalize e : ğ’° k = c at r
  induction r generalizing k
  case refl => rfl
  case trans r _ ih => subst e; cases r; exact ih rfl

theorem parsMtyInv {b} (r : mty â‡’â‹† b) : b = mty := by
  generalize e : mty = a at r
  induction r
  case refl => rfl
  case trans r _ ih => subst e; cases r; simp [ih]

theorem parsPiInv {a b c} (r : pi a b â‡’â‹† c) : âˆƒ a' b', c = pi a' b' âˆ§ a â‡’â‹† a' âˆ§ b â‡’â‹† b' := by
  generalize e : pi a b = c' at r
  induction r generalizing a b <;> subst e
  case refl => exists a, b; repeat constructor
  case trans ih r =>
    cases r with | pi raâ‚ rbâ‚ =>
    let âŸ¨a', b', e, raâ‚‚, rbâ‚‚âŸ© := ih rfl
    exact âŸ¨a', b', e, trans raâ‚ raâ‚‚, trans rbâ‚ rbâ‚‚âŸ©

theorem parsAllInv {a b c} (r : all a b â‡’â‹† c) : âˆƒ a' b', c = all a' b' âˆ§ a â‡’â‹† a' âˆ§ b â‡’â‹† b' := by
  generalize e : all a b = c' at r
  induction r generalizing a b <;> subst e
  case refl => exists a, b; repeat constructor
  case trans ih r =>
    cases r with | all raâ‚ rbâ‚ =>
    let âŸ¨a', b', e, raâ‚‚, rbâ‚‚âŸ© := ih rfl
    exact âŸ¨a', b', e, trans raâ‚ raâ‚‚, trans rbâ‚ rbâ‚‚âŸ©

/-*---------------------------------------
  Confluence via Takahashi's translation
---------------------------------------*-/

@[simp]
def taka : Term â†’ Term
  | pi a b => pi (taka a) (taka b)
  | all a b => all (taka a) (taka b)
  | abs b => abs (taka b)
  | app b a => match b with
    | abs b => subst (taka a +: var) (taka b)
    | b => app (taka b) (taka a)
  | exf b => exf (taka b)
  | t => t

theorem parTaka {a b} (r : a â‡’ b) : b â‡’ taka a := by
  induction r
  any_goals unfold taka; (constructor <;> assumption)
  case Î² ihb iha => apply parCong <;> assumption
  case app r _ ih _ =>
    unfold taka; split
    . cases r; cases ih; apply Par.Î² <;> assumption
    . constructor <;> assumption

theorem diamond {a b c} (râ‚ : a â‡’ b) (râ‚‚ : a â‡’ c) : âˆƒ d, b â‡’ d âˆ§ c â‡’ d :=
  âŸ¨taka a, parTaka râ‚, parTaka râ‚‚âŸ©

/-*--------------------
      a
     / \
    b   c  by diamond
  // \ /
  d   e  by diacon
  \\ //
    f
--------------------*-/

theorem diacon {a b c} (râ‚ : a â‡’â‹† b) (râ‚‚ : a â‡’ c) : âˆƒ d, b â‡’â‹† d âˆ§ c â‡’â‹† d := by
  induction râ‚ generalizing c
  case refl => exact âŸ¨c, parPars râ‚‚, refl câŸ©
  case trans a b d râ‚ _ ih =>
    let âŸ¨e, râ‚ƒ, râ‚„âŸ© := diamond râ‚ râ‚‚
    let âŸ¨f, râ‚…, râ‚†âŸ© := ih râ‚ƒ
    exact âŸ¨f, râ‚…, trans râ‚„ râ‚†âŸ©

/-*---------------------------
     a
   //  \
  b     c  by diacon
   \\ // \\
     e     d  by confluence
      \\ //
        f
---------------------------*-/

theorem confluence {a b c} (râ‚ : a â‡’â‹† b) (râ‚‚ : a â‡’â‹† c) : âˆƒ d, b â‡’â‹† d âˆ§ c â‡’â‹† d := by
  induction râ‚‚ generalizing b
  case refl => exact âŸ¨b, refl b, râ‚âŸ©
  case trans a c d râ‚‚ _ ih =>
    let âŸ¨e, râ‚ƒ, râ‚„âŸ© := diacon râ‚ râ‚‚
    let âŸ¨f, râ‚…, râ‚†âŸ© := ih râ‚„
    exact âŸ¨f, parsTrans râ‚ƒ râ‚…, râ‚†âŸ©

/-*-----------
  Conversion
-----------*-/

def Conv (a : Term) (b : Term) : Prop := âˆƒ c, a â‡’â‹† c âˆ§ b â‡’â‹† c
infix:40 "â‡”" => Conv

theorem parsConv {a b} (r : a â‡’â‹† b) : a â‡” b :=
  âŸ¨b, r, refl bâŸ©

theorem parConv {a b} (r : a â‡’ b) : a â‡” b :=
  parsConv (parPars r)

theorem convRefl {a} : a â‡” a :=
  âŸ¨a, refl a, refl aâŸ©

theorem convSym {a b} : a â‡” b â†’ b â‡” a
  | âŸ¨c, ra, rbâŸ© => âŸ¨c, rb, raâŸ©

theorem convTrans {a b c} : a â‡” b â†’ b â‡” c â†’ a â‡” c
  | âŸ¨_, rac, rbcâŸ©, âŸ¨_, rbd, rcdâŸ© =>
  let âŸ¨e, rce, rdeâŸ© := confluence rbc rbd
  âŸ¨e, parsTrans rac rce, parsTrans rcd rdeâŸ©

theorem convSubst {a b} Ïƒ : a â‡” b â†’ subst Ïƒ a â‡” subst Ïƒ b
  | âŸ¨c, ra, rbâŸ© => âŸ¨subst Ïƒ c, parsSubst Ïƒ ra, parsSubst Ïƒ rbâŸ©

theorem convCong {a a' b b'} : a â‡” a' â†’ b â‡” b' â†’ subst (a +: var) b â‡” subst (a' +: var) b'
  | âŸ¨a'', ra, ra'âŸ©, âŸ¨b'', rb, rb'âŸ© =>
  âŸ¨subst (a'' +: var) b'', parsCong ra rb, parsCong ra' rb'âŸ©

/-*----------------------------
  Constructors for conversion
----------------------------*-/

theorem convPi {a a' b b'} : a â‡” a' â†’ b â‡” b' â†’ pi a b â‡” pi a' b'
  | âŸ¨a'', ra, ra'âŸ©, âŸ¨b'', rb, rb'âŸ© =>
  âŸ¨pi a'' b'', parsPi ra rb, parsPi ra' rb'âŸ©

theorem convAll {a a' b b'} : a â‡” a' â†’ b â‡” b' â†’ all a b â‡” all a' b'
  | âŸ¨a'', ra, ra'âŸ©, âŸ¨b'', rb, rb'âŸ© =>
  âŸ¨all a'' b'', parsAll ra rb, parsAll ra' rb'âŸ©

theorem convAbs {b b'} : b â‡” b' â†’ abs b â‡” abs b'
  | âŸ¨b'', rb, rb'âŸ© => âŸ¨abs b'', parsAbs rb, parsAbs rb'âŸ©

theorem convApp {b b' a a'} : b â‡” b' â†’ a â‡” a' â†’ app b a â‡” app b' a'
  | âŸ¨b'', rb, rb'âŸ©, âŸ¨a'', ra, ra'âŸ© =>
  âŸ¨app b'' a'', parsApp rb ra, parsApp rb' ra'âŸ©

theorem convExf {b b'} : b â‡” b' â†’ exf b â‡” exf b'
  | âŸ¨b'', rb, rb'âŸ© => âŸ¨exf b'', parsExf rb, parsExf rb'âŸ©

/-*------------------------------------
  Inversion principles for conversion
------------------------------------*-/

theorem convğ’°Mty {k} : Â¬ ğ’° k â‡” mty
  | âŸ¨_, rğ’°, rmtyâŸ© =>
  have eğ’° := parsğ’°Inv rğ’°
  have emty := parsMtyInv rmty
  by subst emty; contradiction

theorem convğ’°Pi {c a b} : Â¬ ğ’° c â‡” pi a b
  | âŸ¨_, rğ’°, rpiâŸ© =>
  have eğ’° := parsğ’°Inv rğ’°
  let âŸ¨_, _, epi, _, _âŸ© := parsPiInv rpi
  by subst epi; contradiction

theorem convMtyPi {a b} : Â¬ mty â‡” pi a b
  | âŸ¨_, rmty, rpiâŸ© =>
  let âŸ¨_, _, epi, _, _âŸ© := parsPiInv rpi
  have emty := parsMtyInv rmty
  by subst epi; contradiction

theorem convğ’°Inv {j k} : ğ’° j â‡” ğ’° k â†’ j = k
  | âŸ¨_, ra, rbâŸ© =>
  have eğ’°a := parsğ’°Inv ra
  have eğ’°b := parsğ’°Inv rb
  by subst eğ’°a; injection eğ’°b

theorem convPiInv {aâ‚ aâ‚‚ bâ‚ bâ‚‚} : pi aâ‚ bâ‚ â‡” pi aâ‚‚ bâ‚‚ â†’ aâ‚ â‡” aâ‚‚ âˆ§ bâ‚ â‡” bâ‚‚
  | âŸ¨_, râ‚, râ‚‚âŸ© =>
  let âŸ¨aâ‚', bâ‚', eâ‚, raâ‚, rbâ‚âŸ© := parsPiInv râ‚
  let âŸ¨aâ‚‚', bâ‚‚', eâ‚‚, raâ‚‚, rbâ‚‚âŸ© := parsPiInv râ‚‚
  by subst eâ‚; injection eâ‚‚ with ea eb
     subst ea; subst eb
     exact âŸ¨âŸ¨aâ‚', raâ‚, raâ‚‚âŸ©, âŸ¨bâ‚', rbâ‚, rbâ‚‚âŸ©âŸ©
