import Â«TT-modelÂ».level

open Nat

set_option autoImplicit false
set_option pp.fieldNotation false

variable [lc : LevelClass]

@[simp]
def cons {A : Type} (x : A) (Î¾ : Nat â†’ A) : Nat â†’ A
  | 0 => x
  | n + 1 => Î¾ n
infixr:50 "+:" => cons

/-*------
  Terms
------*-/

inductive Term : Type where
  | var : Nat â†’ Term
  | ğ’° : Term â†’ Term
  | pi : Term â†’ Term â†’ Term
  | abs : Term â†’ Term
  | app : Term â†’ Term â†’ Term
  | mty : Term
  | exf : Term â†’ Term
  | lvl : Term â†’ Term
  | lof : lc.L â†’ Term
open Term

/-*------------------
  Lifting renamings
------------------*-/

@[simp]
def lift (Î¾ : Nat â†’ Nat) : Nat â†’ Nat :=
  zero +: (succ âˆ˜ Î¾)

-- Lifting respects renaming extensionality
omit lc in
theorem liftExt Î¾ Î¶ (h : âˆ€ x, Î¾ x = Î¶ x) : âˆ€ x, lift Î¾ x = lift Î¶ x := by
  intro x; cases x <;> simp [h]

-- Lifting identity renaming does nothing
omit lc in
theorem liftId Î¾ (h : âˆ€ x, Î¾ x = x) : âˆ€ x, lift Î¾ x = x := by
  intro x; cases x <;> simp [h]

-- Lifting composes
omit lc in
theorem liftComp Î¾ Î¶ Ï‚ (h : âˆ€ x, (Î¾ âˆ˜ Î¶) x = Ï‚ x) :
  âˆ€ x, (lift Î¾ âˆ˜ lift Î¶) x = lift Ï‚ x := by
  intro x; cases x <;> simp
  case succ => apply h

-- Lifting commutes with succ
omit lc in
theorem liftSucc Î¾ : âˆ€ x, (lift Î¾ âˆ˜ succ) x = (succ âˆ˜ Î¾) x := by
  intro x; cases x <;> simp

/-*-------------------
  Applying renamings
-------------------*-/

@[simp]
def rename (Î¾ : Nat â†’ Nat) : Term â†’ Term
  | var s => var (Î¾ s)
  | ğ’° a => ğ’° (rename Î¾ a)
  | pi a b => pi (rename Î¾ a) (rename (lift Î¾) b)
  | abs b => abs (rename (lift Î¾) b)
  | app b a => app (rename Î¾ b) (rename Î¾ a)
  | mty => mty
  | exf b => exf (rename Î¾ b)
  | lvl a => lvl (rename Î¾ a)
  | lof k => lof k

-- Renaming extensionality
theorem renameExt Î¾ Î¶ (h : âˆ€ x, Î¾ x = Î¶ x) : âˆ€ s, rename Î¾ s = rename Î¶ s := by
  intro s; induction s generalizing Î¾ Î¶
  all_goals simp; try constructor
  all_goals apply_rules [liftExt]

-- Applying identity renaming does nothing
theorem renameId s : rename id s = s := by
  induction s
  all_goals simp; try constructor
  all_goals try assumption
  all_goals rw [renameExt (0 +: succ) id]
  all_goals apply_rules [liftId]

-- Renamings compose
theorem renameComp' Î¾ Î¶ Ï‚ (h : âˆ€ x, (Î¾ âˆ˜ Î¶) x = Ï‚ x) : âˆ€ s, (rename Î¾ âˆ˜ rename Î¶) s = rename Ï‚ s := by
  intro s; induction s generalizing Î¾ Î¶ Ï‚
  all_goals simp; try constructor
  all_goals apply_rules [liftComp]

-- Renamings compose (extensionally)
theorem renameComp Î¾ Î¶ s : rename Î¾ (rename Î¶ s) = rename (Î¾ âˆ˜ Î¶) s := by
  apply renameComp'; simp

/-*----------------------
  Lifting substitutions
----------------------*-/

@[simp]
def up (Ïƒ : Nat â†’ Term) : Nat â†’ Term :=
  var 0 +: (rename succ âˆ˜ Ïƒ)
prefix:95 "â‡‘" => up

-- Lifting twice pushes renamings inwards
theorem upUp Ïƒ x : (â‡‘ â‡‘ Ïƒ) x = (var 0 +: var 1 +: (rename succ âˆ˜ rename succ âˆ˜ Ïƒ)) x := by
  cases x; rfl
  case succ n => cases n <;> rfl

-- Lifting var "substitution" does nothing
theorem upId Ïƒ (h : âˆ€ x, Ïƒ x = var x) : âˆ€ x, (â‡‘ Ïƒ) x = var x := by
  intro n; cases n <;> simp [h]

-- Lifting respects subsitution extensionality
theorem upExt Ïƒ Ï„ (h : âˆ€ x, Ïƒ x = Ï„ x) : âˆ€ x, (â‡‘ Ïƒ) x = (â‡‘ Ï„) x := by
  intro n; cases n <;> simp [h]

-- Lifting commutes with composition
theorem upLift Î¾ Ïƒ Ï„ (h : âˆ€ x, (Ïƒ âˆ˜ Î¾) x = Ï„ x) : âˆ€ x, (â‡‘ Ïƒ âˆ˜ lift Î¾) x = (â‡‘ Ï„) x := by
  intro n; cases n <;> simp [â† h]

-- Lifting commutes with succ
theorem upSucc Ïƒ : âˆ€ x, (â‡‘ Ïƒ âˆ˜ succ) x = (rename succ âˆ˜ Ïƒ) x := by
  intro n; cases n <;> simp

-- Lifting commutes with renaming
theorem upRename Î¾ Ïƒ Ï„ (h : âˆ€ x, (rename Î¾ âˆ˜ Ïƒ) x = Ï„ x) : âˆ€ x, (rename (lift Î¾) âˆ˜ â‡‘ Ïƒ) x = (â‡‘ Ï„) x := by
  intro n; cases n; simp
  case succ n => calc
    (rename (lift Î¾) âˆ˜ rename succ) (Ïƒ n)
      = rename (lift Î¾ âˆ˜ succ) (Ïƒ n)      := by simp [renameComp]
    _ = (rename (succ âˆ˜ Î¾)) (Ïƒ n)         := by rfl
    _ = (rename succ âˆ˜ rename Î¾) (Ïƒ n)    := by simp [renameComp]
    _ = (rename succ (rename Î¾ (Ïƒ n)))    := by rfl
    _ = rename succ (Ï„ n)                 := by rw [â† h]; rfl

/-*-----------------------
  Applying substitutions
-----------------------*-/

@[simp]
def subst (Ïƒ : Nat â†’ Term) : Term â†’ Term
  | var s => Ïƒ s
  | ğ’° a => ğ’° (subst Ïƒ a)
  | pi a b => pi (subst Ïƒ a) (subst (â‡‘ Ïƒ) b)
  | abs b => abs (subst (â‡‘ Ïƒ) b)
  | app b a => app (subst Ïƒ b) (subst Ïƒ a)
  | mty => mty
  | exf b => exf (subst Ïƒ b)
  | lvl a => lvl (subst Ïƒ a)
  | lof k => lof k

-- Substitution extensionality
theorem substExt Ïƒ Ï„ (h : âˆ€ x, Ïƒ x = Ï„ x) : âˆ€ s, subst Ïƒ s = subst Ï„ s := by
  intro s; induction s generalizing Ïƒ Ï„
  all_goals simp; try constructor
  all_goals apply_rules [upExt]

-- Applying var "substitution" does nothing
theorem substId' Ïƒ (h : âˆ€ x, Ïƒ x = var x) : âˆ€ s, subst Ïƒ s = s := by
  intro s; induction s generalizing Ïƒ
  all_goals simp; try constructor
  all_goals apply_rules [upId]

-- Substitution/renaming compositionality
theorem substRename' Î¾ Ïƒ Ï„ (h : âˆ€ x, (Ïƒ âˆ˜ Î¾) x = Ï„ x) : âˆ€ s, subst Ïƒ (rename Î¾ s) = subst Ï„ s := by
  intro s; induction s generalizing Î¾ Ïƒ Ï„
  all_goals simp; try constructor
  all_goals apply_rules [upLift]

-- Renaming/substitution compositionality
theorem renameSubst' Î¾ Ïƒ Ï„ (h : âˆ€ x, (rename Î¾ âˆ˜ Ïƒ) x = Ï„ x) : âˆ€ s, rename Î¾ (subst Ïƒ s) = subst Ï„ s := by
  intro s; induction s generalizing Î¾ Ïƒ Ï„
  all_goals simp; try constructor
  all_goals apply_rules [upRename]

-- Lifting commutes with substitution
theorem upSubst Ï Ïƒ Ï„ (h : âˆ€ x, (subst Ï âˆ˜ Ïƒ) x = Ï„ x) : âˆ€ x, (subst (â‡‘ Ï) âˆ˜ (â‡‘ Ïƒ)) x = (â‡‘ Ï„) x := by
  intro n; cases n; rfl
  case succ n => calc
    (subst (â‡‘ Ï) âˆ˜ rename succ) (Ïƒ n)
      = subst (â‡‘ Ï âˆ˜ succ) (Ïƒ n)      := by rw [â† substRename']; rfl; simp
    _ = subst (rename succ âˆ˜ Ï) (Ïƒ n) := by rfl
    _ = (rename succ âˆ˜ subst Ï) (Ïƒ n) := by rw [â† renameSubst']; rfl; simp
    _ = rename succ (subst Ï (Ïƒ n))   := by rfl
    _ = rename succ (Ï„ n)             := by rw [â† h]; rfl

-- Substitution compositionality
theorem substComp' Ï Ïƒ Ï„ (h : âˆ€ x, (subst Ï âˆ˜ Ïƒ) x = Ï„ x) : âˆ€ s, (subst Ï âˆ˜ subst Ïƒ) s = subst Ï„ s := by
  intro s; induction s generalizing Ï Ïƒ Ï„
  all_goals simp; try constructor
  all_goals apply_rules [upSubst]

/-*----------------------------------------------
  Substitution & renaming lemmas, extensionally
----------------------------------------------*-/

def substId : âˆ€ s, subst var s = s :=
  substId' var (by simp)

def substRename Î¾ Ïƒ : âˆ€ s, subst Ïƒ (rename Î¾ s) = subst (Ïƒ âˆ˜ Î¾) s :=
  substRename' _ _ (Ïƒ âˆ˜ Î¾) (by simp)

def renameSubst Î¾ Ïƒ : âˆ€ s, rename Î¾ (subst Ïƒ s) = subst (rename Î¾ âˆ˜ Ïƒ) s :=
  renameSubst' _ _ (rename Î¾ âˆ˜ Ïƒ) (by simp)

def substComp Ïƒ Ï„ : âˆ€ s, (subst Ïƒ âˆ˜ subst Ï„) s = subst (subst Ïƒ âˆ˜ Ï„) s :=
  substComp' _ _ (subst Ïƒ âˆ˜ Ï„) (by simp)

/-*-------------------------------------------------
  Handy dandy derived renaming substitution lemmas
-------------------------------------------------*-/

theorem renameLiftRename Î¾ a : rename succ (rename Î¾ a) = rename (lift Î¾) (rename succ a) := by
  calc
    rename succ (rename Î¾ a)
      = rename (succ âˆ˜ Î¾) a             := by rw [renameComp]
    _ = rename (lift Î¾ âˆ˜ succ) a        := by rw [renameExt]; exact liftSucc Î¾
    _ = rename (lift Î¾) (rename succ a) := by rw [â† renameComp]

theorem renameUpSubst Ïƒ a : rename succ (subst Ïƒ a) = subst (â‡‘ Ïƒ) (rename succ a) := by
  calc
    rename succ (subst Ïƒ a)
      = subst (rename succ âˆ˜ Ïƒ) a   := by rw [renameSubst]
    _ = subst (â‡‘ Ïƒ âˆ˜ succ) a        := by rw [substExt]; exact upSucc Ïƒ
    _ = subst (â‡‘ Ïƒ) (rename succ a) := by rw [substRename]

theorem renameDist Î¾ a s : subst (rename Î¾ a +: var) (rename (lift Î¾) s) = rename Î¾ (subst (a +: var) s) := by
  calc
    subst (rename Î¾ a +: var) (rename (lift Î¾) s)
    _ = subst ((rename Î¾ a +: var) âˆ˜ lift Î¾) s := by rw [substRename]
    _ = subst (rename Î¾ âˆ˜ (a +: var)) s        := by apply substExt; intro n; cases n <;> rfl
    _ = rename Î¾ (subst (a +: var) s)          := by rw [â† renameSubst]

theorem substDrop a b : b = subst (a +: var) (rename succ b) := by
  calc
    b = subst var b                      := by rw [substId]
    _ = subst (a +: var) (rename succ b) := by rw [substRename]; rfl

theorem substUnion Ïƒ a s : subst (a +: Ïƒ) s = subst (a +: var) (subst (â‡‘ Ïƒ) s) := by
  calc
    subst (a +: Ïƒ) s
      = subst (subst (a +: var) âˆ˜ (var 0 +: (rename succ âˆ˜ Ïƒ))) s :=
        by apply substExt; intro n; cases n <;> simp; rw [â† substDrop]
    _ = subst (a +: var) (subst (â‡‘ Ïƒ) s) :=
        by rw [â† substComp]; rfl

theorem substDist Ïƒ a s : subst (subst Ïƒ a +: var) (subst (â‡‘ Ïƒ) s) = subst Ïƒ (subst (a +: var) s) := by
  calc
    subst (subst Ïƒ a +: var) (subst (â‡‘ Ïƒ) s)
      = subst (subst Ïƒ a +: Ïƒ) s       := by rw [â† substUnion]
    _ = subst (subst Ïƒ âˆ˜ (a +: var)) s := by apply substExt; intro n; cases n <;> rfl
    _ = (subst Ïƒ âˆ˜ subst (a +: var)) s := by rw [â† substComp]

/-*------------------------
  Contexts and membership
------------------------*-/

inductive Ctxt : Type where
  | nil : Ctxt
  | cons : Ctxt â†’ Term â†’ Ctxt
notation:50 "â¬" => Ctxt.nil
infixl:50 "âˆ·" => Ctxt.cons

inductive In : Nat â†’ Term â†’ Ctxt â†’ Prop where
  | here {Î“ A} : In 0 (rename succ A) (Î“ âˆ· A)
  | there {Î“ x A B} : In x A Î“ â†’ In (succ x) (rename succ A) (Î“ âˆ· B)
notation:40 Î“:41 "âˆ‹" x:41 "âˆ¶" A:41 => In x A Î“

theorem inHere {Î“ A A'} (e : A' = rename succ A) : (Î“ âˆ· A) âˆ‹ 0 âˆ¶ A' := by
  subst e; apply In.here

theorem inThere {Î“ x A A' B} (h : Î“ âˆ‹ x âˆ¶ A) (e : A' = rename succ A) : Î“ âˆ· B âˆ‹ succ x âˆ¶ A' := by
  subst e; apply In.there; assumption
